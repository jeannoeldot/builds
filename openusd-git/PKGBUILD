# Maintainer: Moi <name AT fai DOT fr>

pkgname=openusd-git
pkgver=23.11.20231027
pkgrel=3
_ver_release="23.11"
_name_release="OpenUSD-${_ver_release}"
pkgdesc="Universal Scene Description"
url="http://www.openusd.org/"
arch=('x86_64')
license=('Apache-2.0')
depends=('boost-libs' 'glfw'
         'onetbb'
         'materialx'
         'openexr'
         'opensubdiv-git'
         'openimageio-git'
         'openvdb-git'
         'python'
         'python-jinja'
)
makedepends=('boost' 'cmake'
)
conflicts=('usd' 'openusd')
provides=('usd' 'openusd')
options=('!debug')
source=("OpenUSD-v${_ver_release}.tar.gz"
        "blender-usd-diff.patch"
        "blender-usd_core_profile-diff.patch"
        "usd-tbb.patch"
)
sha256sums=('2add389b121568f3dfb9b7e4f4551a6c8445ae353f1725a0753c8ce5639c4f83'
            'eeac5135796c180f78a45abb381dfff7c7784b71bc06c4b2882def6c79f397d1'
            'd082557c30083bfd4244f040dd4c14a643039284f34e0841ffe03d97a0213777'
            'e4a1378a2ce34edbd65f016689cbca0dc37dc2b1c2ddfd837724078ed231063c'
)

prepare() {
  cd "${_name_release}"
  # Compatible avec onetbb 2021.11.0
  patch -Np1 -i "${srcdir}/usd-tbb.patch"
  # blender/build_files/build_environment/patches/usd.diff
  patch -Np1 -i "${srcdir}/blender-usd-diff.patch"
  # blender/build_files/build_environment/patches/usd_core_profile.diff
  patch -Np1 -i "${srcdir}/blender-usd_core_profile-diff.patch"

  _pyver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  # Change directories to standard
  sed -i 's|plugin/usd|lib/usd/plugin|g' \
      cmake/macros/{Private,Public}.cmake
  sed -i 's|/python|/python'$_pyver'/site-packages|g' \
      cmake/macros/Private.cmake
  sed -i 's|lib/python/pxr|/usr/lib/python'$_pyver'/site-packages/pxr|' \
      cmake/macros/{Private,Public}.cmake pxr/usdImaging/usdviewq/CMakeLists.txt

  sed -i 's|/pxrConfig.cmake|/lib/cmake/pxr/pxrConfig.cmake|g' \
      pxr/CMakeLists.txt
  sed -i 's|${CMAKE_INSTALL_PREFIX}|${CMAKE_INSTALL_PREFIX}/lib/cmake/pxr|g' \
      pxr/CMakeLists.txt
  sed -i 's|"cmake"|"lib/cmake/pxr"|g' \
      pxr/CMakeLists.txt
  sed -i 's|${PXR_CMAKE_DIR}/cmake|${PXR_CMAKE_DIR}|g' \
      pxr/pxrConfig.cmake.in
  sed -i 's|${PXR_CMAKE_DIR}/include|/usr/include|g' \
      pxr/pxrConfig.cmake.in
  sed -i 's|EXACT COMPONENTS|COMPONENTS|g' \
      pxr/pxrConfig.cmake.in

  # Further drop shebangs line for some py files
  sed -r -i '1{/^#!/d}' \
      pxr/usd/sdr/shaderParserTestUtils.py \
      pxr/usd/usdUtils/updateSchemaWithSdrNode.py \
      pxr/usdImaging/usdviewq/usdviewApi.py
}

build() {
  cd "${_name_release}"

  cmake \
    -Bbuild \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DPXR_ENABLE_PYTHON_SUPPORT=ON \
    -DPXR_USE_PYTHON_3=ON \
    -DPXR_BUILD_IMAGING=ON \
    -DPXR_BUILD_TESTS=OFF \
    -DPXR_BUILD_EXAMPLES=OFF \
    -DPXR_BUILD_TUTORIALS=OFF \
    -DPXR_BUILD_USDVIEW=OFF \
    -DPXR_ENABLE_HDF5_SUPPORT=OFF \
    -DPXR_ENABLE_MATERIALX_SUPPORT=ON \
    -DPXR_ENABLE_OPENVDB_SUPPORT=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python \
    -DPXR_BUILD_MONOLITHIC=ON \
    -DPXR_ENABLE_OSL_SUPPORT=OFF \
    -DPXR_ENABLE_GL_SUPPORT=ON \
    -DPXR_BUILD_OPENIMAGEIO_PLUGIN=ON \
    -DPXR_BUILD_OPENCOLORIO_PLUGIN=OFF \
    -DPXR_ENABLE_PTEX_SUPPORT=OFF \
    -DPXR_BUILD_USD_TOOLS=OFF \
    -DCMAKE_DEBUG_POSTFIX=_d \
    -DBUILD_SHARED_LIBS=ON \

  find . -type f -exec sed -i -e 's/\-O3//g' {} \;
  make -C build
}

package() {
  cd "${_name_release}"
  DESTDIR="${pkgdir}" make -C build install

  sed -i 's|${PXR_CMAKE_DIR}/cmake|${PXR_CMAKE_DIR}|g' \
    ${pkgdir}/usr/lib/cmake/pxr/pxrConfig.cmake
  sed -i 's/_IMPORT_PREFIX ""/_IMPORT_PREFIX "\/usr"/' \
    ${pkgdir}/usr/lib/cmake/pxr/pxrTargets.cmake

  #license
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
