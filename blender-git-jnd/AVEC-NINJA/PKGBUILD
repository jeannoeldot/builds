# Maintainer: Name <name@fai.com>

pkgname=blender-git-jnd
pkgver=5.1.0.22.20260129
pkgrel=2
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64')
license=('GPL-3.0-or-later')
url="https://blender.org/"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'
          'libpng' 'openjpeg2' 'libtiff' 'openexr' 'libwebp' 'libjpeg-turbo'
          'python'
          'ffmpeg' 'fftw'
          'openal' 'freetype2' 'libsndfile' 'jack2' 'pipewire-pulse' 'pipewire'
          'libglvnd' 'glfw' 'libxi'
          'onetbb' 'draco' 'openxr'
          'python-numpy' 'python-psutil' 'python-requests' 'python-zstandard' 'pystring'
          'python-fastjsonschema' 'python-cattrs'
          'llvm-libs'
          'potrace' 'pugixml' 'libharu' 'libepoxy'
          'libdecor' 'libxkbcommon'
          'fribidi' 'harfbuzz' 'minizip-ng' 'pybind11' 'shaderc'
          'vulkan-headers' 'vulkan-icd-loader' 'vulkan-driver'
          'opencolorio' 'libdeflate'
          'zstd'
          'openimageio-jnd' 'fmt'
          'openshadinglanguage-jnd'
          'opensubdiv-jnd' 'openvdb-jnd' 'alembic-jnd'
          'embree-jnd' 'openimagedenoise-jnd'
          'openpgl-jnd'
          'manifold-jnd'
          'materialx' 'openusd-jnd'
)
makedepends=('git' 'git-lfs' 'cmake' 'mesa' 'llvm'
             'cuda' 'nvidia-utils'
             'wayland-protocols'
             'mold'
             'ninja'
)
optdepends=('cuda: cycles renderer cuda support')
conflicts=('blender' 'blender-git')
provides=('blender')
options=('!lto' '!debug')
_pkgname_git=blender-git
source=("$_pkgname_git-$pkgver.tar.zst"
        "OptiX-9.1-Include.zip"
)
sha256sums=('09f3b3f68f8996a7350deed5bccac376f21ce513e7e76ced6d609ce799bab91e'
            'ff2da3939b52673ca9ac976e099dfcbf1ba195af66c2704d21d0eaeafd57ee57'
)

_gitname="blender"

prepare() {
  cd "${_gitname}"

  ## Cycles: Workaround for performance loss with the CUDA 9.0 SDK.
  ## Commit 1febc858559c054603073301a6c6dae44c737830 du 21/11/2017. Stefan Werner
  #sed -i -e "s|define CUDA_KERNEL_MAX_REGISTERS 48|define CUDA_KERNEL_MAX_REGISTERS 64|" intern/cycles/kernel/kernels/cuda/kernel_config.h

  # Buildinfo : Workaround for HASH, BRANCH et COMMIT_TIMESTAMP
  sed -i -e 's|MY_WC_HASH "unknown"|MY_WC_HASH "3f0450f4d9d6"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_BRANCH "unknown"|MY_WC_BRANCH "main"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_COMMIT_TIMESTAMP 0|MY_WC_COMMIT_TIMESTAMP 1769680336|' build_files/cmake/buildinfo.cmake
}

_get_pyver() {
  python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

build() {

  # In general, we want to list all real archs (sm_XX) and the latest virtual arch (compute_XX) for future PTX compatibility.
  # Valid values can be discovered from nvcc --help
  local cuda_archs="sm_120;compute_120"

  local cmake_options=(
    -B build
    -C "${_gitname}/build_files/cmake/config/blender_release.cmake"
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/usr
    -D WITH_LINKER_MOLD=ON
    -D CUDA_HOST_COMPILER="$NVCC_CCBIN"
    -D CYCLES_CUDA_BINARIES_ARCH="$cuda_archs"
    -D OPTIX_ROOT_DIR="$srcdir"
    -D WITH_DOC_MANPAGE=OFF
    -D WITH_CYCLES_DEVICE_HIP=OFF
    -D WITH_CYCLES_DEVICE_HIPRT=OFF
    -D WITH_CYCLES_HIP_BINARIES=OFF
    -D WITH_CYCLES_DEVICE_ONEAPI=OFF
    -D WITH_CYCLES_ONEAPI_BINARIES=OFF
    -D PYTHON_VERSION="$(_get_pyver)"
    -D WITH_INSTALL_PORTABLE=OFF
    -D WITH_PYTHON_INSTALL=OFF
    -G Ninja
    -S "${_gitname}"
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build

  cd "${_gitname}"
  python -m compileall "${pkgdir}/usr/share/blender"
  python -O -m compileall "${pkgdir}/usr/share/blender"
}
