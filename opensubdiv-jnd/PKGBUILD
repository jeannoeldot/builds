# Maintainer: Name <name@fai.com>

pkgname=opensubdiv-jnd
pkgver=3_7_0.20251030
pkgrel=1
_ver_release="3_7_0"
_name_release="OpenSubdiv-${_ver_release}"
pkgdesc="High performance subdivision surface (subdiv) evaluation on massively parallel CPU and GPU architectures"
arch=('x86_64')
license=('LicenseRef-custom:TOST')
url="http://graphics.pixar.com/opensubdiv"
depends=('glfw-x11' 'onetbb' 'libxcursor' 'xorg-xrandr' 'libxinerama' 'libxi'
)
makedepends=('cmake' 'mesa' 'python')
conflicts=('opensubdiv' 'opensubdiv-git')
provides=('opensubdiv')
options=('!debug' '!lto' 'staticlibs')
source=("OpenSubdiv-v${_ver_release}.tar.gz"
)
sha256sums=('f843eb49daf20264007d807cbc64516a1fed9cdb1149aaf84ff47691d97491f9'
)

build() {
  cd "${_name_release}"
  
  cmake \
        -B build \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_SKIP_RPATH=ON \
        -D NO_PTEX=1 \
        -D NO_CLEW=1 \
        -D NO_CUDA=1 \
        -D NO_OPENCL=1 \
        -D NO_DOC=1 \
        -D NO_TUTORIALS=1 \
        -D NO_REGRESSION=1 \
        -D NO_EXAMPLES=1 \
        -D NO_METAL=1
  find . -type f -exec sed -i -e 's/\-O3//g' {} \;
  make -C build
}

package() {
  cd "${_name_release}"
  DESTDIR="${pkgdir}" make -C build install

  # license
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
