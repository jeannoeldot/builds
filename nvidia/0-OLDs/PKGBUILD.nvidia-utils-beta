# Maintainer: Dan Vratil <vratil@progdansoft.com>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Maintainer: JND

pkgname=nvidia-utils-beta
pkgver=260.19.06
pkgrel=1
pkgdesc="NVIDIA beta drivers utilities and libraries."
arch=('x86_64')
url="http://www.nvidia.com/"
depends=('xorg-server' 'libxvmc')
optdepends=('gtk2: nvidia-settings'
	    'pkgconfig: nvidia-xconfig'
	    'cuda-toolkit: CUDA toolkit with cuda headers'
	    'opencl-headers: OpenCL header files')
conflicts=('libgl' 'nvidia-utils')
provides=('libgl' "nvidia-utils=${pkgver}-${pkgrel}")
license=('custom')
install=nvidia-utils-beta.install
options=('!strip')
backup=('etc/X11/xorg.conf.d/20-nvidia.conf')

_arch='x86_64'
_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
md5sums=('69f00bf3f8f381a6a870d9ad341e3a2e')

source[1]='20-nvidia.conf'
md5sums[1]='541cb36684b6764ccba73d7afe268bf5'

build() {
	cd $srcdir
	sh ${_pkg}.run --extract-only
}

package() {
	cd $srcdir/${_pkg}

	# X driver
	install -D -m755 nvidia_drv.so $pkgdir/usr/lib/xorg/modules/drivers/nvidia_drv.so
	# GLX extension module for X
	install -D -m755 libglx.so.$pkgver $pkgdir/usr/lib/xorg/modules/extensions/libglx.so.$pkgver
	ln -s libglx.so.$pkgver $pkgdir/usr/lib/xorg/modules/extensions/libglx.so	# X doesn't find glx otherwise
	# OpenGL library
	install -D -m755 libGL.so.$pkgver $pkgdir/usr/lib/libGL.so.$pkgver
	# OpenGL core library
	install -D -m755 libnvidia-glcore.so.$pkgver $pkgdir/usr/lib/libnvidia-glcore.so.$pkgver
	# XvMC
	install -D -m644 libXvMCNVIDIA.a $pkgdir/usr/lib/libXvMCNVIDIA.a
	install -D -m755 libXvMCNVIDIA.so.$pkgver $pkgdir/usr/lib/libXvMCNVIDIA.so.$pkgver
	# VDPAU
	install -D -m755 libvdpau_nvidia.so.$pkgver $pkgdir/usr/lib/vdpau/libvdpau_nvidia.so.$pkgver
	# CUDA
	install -D -m755 libcuda.so.$pkgver $pkgdir/usr/lib/libcuda.so.$pkgver
	# nvidia-tls library
	install -D -m755 tls/libnvidia-tls.so.$pkgver $pkgdir/usr/lib/libnvidia-tls.so.$pkgver
	# OpenCL
	install -D -m755 libnvidia-compiler.so.$pkgver $pkgdir/usr/lib/libnvidia-compiler.so.$pkgver
	install -D -m755 libOpenCL.so.1.0.0 $pkgdir/usr/lib/libOpenCL.so.1.0.0
	install -D -m644 nvidia.icd $pkgdir/etc/OpenCL/vendors/nvidia.icd

	# Install libraries
	install -D -m755 libnvidia-wfb.so.$pkgver $pkgdir/usr/lib/libnvidia-wfb.so.$pkgver
	install -D -m755 libnvcuvid.so.$pkgver $pkgdir/usr/lib/libnvcuvid.so.$pkgver
	install -D -m755 libnvidia-cfg.so.$pkgver $pkgdir/usr/lib/libnvidia-cfg.so.$pkgver

	# create soname links
	cd ${pkgdir}/usr/lib/
	ln -s libOpenCL.so.1.0.0 libOpenCL.so.1
	ln -s libOpenCL.so.1 libOpenCL.so
	ln -s libGL.so.${pkgver} libGL.so.1
	ln -s libGL.so.${pkgver} libGL.so
	ln -s libnvidia-glcore.so.${pkgver} libnvidia-glcore.so.1
	ln -s libnvidia-glcore.so.${pkgver} libnvidia-glcore.so
	ln -s libnvidia-cfg.so.${pkgver} libnvidia-cfg.so.1
	ln -s libnvidia-cfg.so.${pkgver} libnvidia-cfg.so
	ln -s libnvidia-compiler.so.${pkgver} libnvidia-compiler.so.1
	ln -s libnvidia-compiler.so.${pkgver} libnvidia-compiler.so
	ln -s libnvidia-tls.so.${pkgver} libnvidia-tls.so.1
	ln -s libnvidia-tls.so.${pkgver} libnvidia-tls.so
	ln -s libcuda.so.${pkgver} libcuda.so.1
	ln -s libcuda.so.${pkgver} libcuda.so
	ln -s libvdpau_nvidia.so.${pkgver} vdpau/libvdpau_nvidia.so.1
	ln -s libvdpau_nvidia.so.${pkgver} vdpau/libvdpau_nvidia.so
	ln -s libnvcuvid.so.${pkgver} libnvcuvid.so.1
	ln -s libnvcuvid.so.${pkgver} libnvcuvid.so
	ln -s libnvidia-wfb.so.${pkgver} libnvidia-wfb.so.1
	ln -s libnvidia-wfb.so.${pkgver} libnvidia-wfb.so
	ln -s libXvMCNVIDIA.so.${pkgver} libXvMCNVIDIA_dynamic.so.1
	ln -s libXvMCNVIDIA.so.${pkgver} libXvMCNVIDIA_dynamic.so

	cd $srcdir/${_pkg}

	# nvidia-xconfig
	install -D -m755 nvidia-xconfig $pkgdir/usr/bin/nvidia-xconfig
	install -D -m644 nvidia-xconfig.1.gz $pkgdir/usr/share/man/man1/nvidia-xconfig.1.gz
	# nvidia-settings
	install -D -m755 nvidia-settings $pkgdir/usr/bin/nvidia-settings
	install -D -m644 nvidia-settings.1.gz $pkgdir/usr/share/man/man1/nvidia-settings.1.gz
	install -D -m644 nvidia-settings.desktop $pkgdir/usr/share/applications/nvidia-settings.desktop
	install -D -m644 nvidia-settings.png $pkgdir/usr/share/pixmaps/nvidia-settings.png
	sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i $pkgdir/usr/share/applications/nvidia-settings.desktop
	# nvidia-bug-report
	install -D -m755 nvidia-bug-report.sh $pkgdir/usr/bin/nvidia-bug-report.sh
	# nvidia-smi
	install -D -m755 nvidia-smi $pkgdir/usr/bin/nvidia-smi
	install -D -m644 nvidia-smi.1.gz $pkgdir/usr/share/man/man1/nvidia-smi.1.gz
	# Install license
	install -D -m644 LICENSE $pkgdir/usr/share/licenses/nvidia/LICENSE
	ln -s nvidia $pkgdir/usr/share/licenses/nvidia-utils
	# Install readme
	install -D -m644 README.txt $pkgdir/usr/share/doc/nvidia/README
	# Install Changelog
	install -D -m644 NVIDIA_Changelog $pkgdir/usr/share/doc/nvidia/NVIDIA_Changelog
	ln -s nvidia $pkgdir/usr/share/doc/nvidia-utils

	# Install xorg.conf.d file for nvidia autodetection in xorg.conf-less configurations
	install -D -m644 $srcdir/20-nvidia.conf "${pkgdir}/etc/X11/xorg.conf.d/20-nvidia.conf"
}


