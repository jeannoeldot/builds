# Maintainer: Name <name@fai.com>

pkgname=openshadinglanguage-jnd
pkgver=1.14.5.1.20250503
pkgrel=3
_name_release="OpenShadingLanguage-1.14.5.1"
_ver_release="v1.14.5.1"
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/AcademySoftwareFoundation/OpenShadingLanguage"
license=('BSD-3-Clause')
depends=('imath' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'llvm-libs'
         'qt6-base'
         'clang'
         'python' 'pugixml' 'pybind11'
         'openimageio-jnd' 'fmt'
)
makedepends=('cmake' 'llvm'
             'robin-map'
)
conflicts=('openshadinglanguage' 'openshadinglanguage-git')
provides=('openshadinglanguage')
options=('!debug')
source=("OpenShadingLanguage-${_ver_release}.tar.gz"
        "Add-missing-cstdint-include-for-GCC-15.patch"
        "osl-llvm20.patch"
)

sha256sums=('409676f5a53a74079eb890a04cf3a2735aa869570e4462798ba974753272a786'
            '773e997749d25076a129e004ce784e5191f94f650788c4f951ef0e3aae3c6d45'
            '7bb89c68cc4466a70b6d32a69a94c6fcb2c9167295b06173f269bd177503ba9d'
)

prepare() {
  cd "${_name_release}"

  # Add missing cstdint include for GCC 15
  patch -Np1 -i "${srcdir}/Add-missing-cstdint-include-for-GCC-15.patch"
  # llvm 20
  patch -Np1 -i "${srcdir}/osl-llvm20.patch"
}

build() {
  cd "${_name_release}"

  cmake \
      -Bbuild \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_CXX_STANDARD=17 \
      -DLLVM_STATIC=OFF \
      -DUSE_SIMD=avx2 \
      -DUSE_CCACHE=OFF
  make -C build
}

package() {
  cd "${_name_release}"

  DESTDIR="${pkgdir}" make -C build install

  # license
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # shaders
  # Ugly hacks /usr/share/OSL/shaders/*.oso
  for _oso in $(find "${pkgdir}"/usr/share/OSL/shaders/*.oso -type f)
  do
    sed -i "s|/build/${pkgname}/src/${_name_release}/src/|/usr/share/OSL/|g" ${_oso}
    sed -i "s|/build/${pkgname}/src/${_name_release}/build/src/|/usr/share/OSL/|g" ${_oso}
  done

  # Fix <File> exists in a non-standard directory
  rm -rf "${pkgdir}"/usr/build-scripts
  rm -rf "${pkgdir}"/usr/cmake
}
