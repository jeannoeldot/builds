# Maintainer: Name <name@fai.com>

pkgname=openshadinglanguage-jnd
pkgver=1.14.7.0.20250803
pkgrel=7
_name_release="OpenShadingLanguage-1.14.7.0"
_ver_release="v1.14.7.0"
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/AcademySoftwareFoundation/OpenShadingLanguage"
license=('BSD-3-Clause')
depends=('imath' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'llvm-libs'
         'qt6-base'
         'clang'
         'python' 'pugixml' 'pybind11'
         'openimageio-jnd' 'fmt'
)
makedepends=('cmake' 'llvm'
             'robin-map'
)
conflicts=('openshadinglanguage' 'openshadinglanguage-git')
provides=('openshadinglanguage')
options=('!debug')
source=("OpenShadingLanguage-${_ver_release}.tar.gz"
        "openshadinglanguage-1.14.7-llvm-21.1.3.patch"
)

sha256sums=('c4c2ddd3b537cd3a90d50fdd0753614fa24100698d5632b9f1108bd8b505e56c'
            '5feee62bf4d4a24276ee87560d6c93c4f4aef90f51439be7fca4f6acba20cfe3'
)

prepare() {
  cd "${_name_release}"
  # # compatibility with the latest llvm
  patch -Np1 -i "${srcdir}/openshadinglanguage-1.14.7-llvm-21.1.3.patch"
}


build() {
  cd "${_name_release}"

  cmake \
      -Bbuild \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_CXX_STANDARD=17 \
      -DLLVM_STATIC=OFF \
      -DUSE_SIMD=avx2 \
      -DUSE_CCACHE=OFF
  make -C build
}

package() {
  cd "${_name_release}"

  DESTDIR="${pkgdir}" make -C build install

  # license
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # shaders
  # Ugly hacks /usr/share/OSL/shaders/*.oso
  for _oso in $(find "${pkgdir}"/usr/share/OSL/shaders/*.oso -type f)
  do
    sed -i "s|/build/${pkgname}/src/${_name_release}/src/|/usr/share/OSL/|g" ${_oso}
    sed -i "s|/build/${pkgname}/src/${_name_release}/build/src/|/usr/share/OSL/|g" ${_oso}
  done

  # Fix <File> exists in a non-standard directory
  rm -rf "${pkgdir}"/usr/build-scripts
  rm -rf "${pkgdir}"/usr/cmake
}
