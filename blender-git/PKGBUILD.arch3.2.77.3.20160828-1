# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=blender-git
pkgver=2.77.3.20160828
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('i686' 'x86_64')
license=('GPL')
url="http://blender.org/"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info' 'xdg-utils'
          'libjpeg' 'libpng' 'openjpeg' 'libtiff' 'openexr' 
          'python'
          'nvidia-libgl'
          'libsamplerate' 'ffmpeg' 'fftw' 'freeglut'
          'openal' 'freetype2' 'libsndfile'
          'sdl2'
          'glew' 'glfw-x11'
          'libxi' 'libxmu'
          'boost-libs'
          'gettext'
          'jemalloc'
          'opencolorio'
          'opencollada-git' 'openshadinglanguage-git' 'openimageio-git'
          'opensubdiv-git'
          'openvdb-git'
)
makedepends=('cmake' 'boost' 'mesa' 'llvm35'
             'cuda'
             'opencl-headers'
             'git'
             'gcc5'
)
optdepends=('cuda: cycles renderer cuda support')
provides=('blender')
conflicts=('blender')
options=('!strip')
install="blender.install"
source=("$pkgname-$pkgver.tar.gz"
        'fix-std-isfinite-has-not-been-declared.patch'
)
sha256sums=('30d7eab746e8c36de539d929613ef1a647740496b9074b17e89979e4f1ed52b9'
            '8d1fcb48f1c21d7bb040239fce6fbedc69a30ff6de6a5e491154047bacebbbbe'
)

_gitname="blender"

prepare() {
  cd "${_gitname}"
  # fix erreur : 
  # rayobject_rtbuild.cpp:46:12: error: 'std::isfinite' has not been declared
  patch -Np1 -i "${srcdir}/fix-std-isfinite-has-not-been-declared.patch"
}

build() {
  cd "${_gitname}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -DCMAKE_C_COMPILER='/usr/bin/gcc-5' \
        -DCMAKE_CXX_COMPILER='/usr/bin/g++-5' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_BUILDINFO=ON \
        -DWITH_FFTW3=ON \
        -DWITH_GAMEENGINE=OFF \
        -DWITH_OPENCOLORIO=ON \
        -DWITH_OPENSUBDIV=ON \
        -DWITH_OPENVDB=ON \
        -DWITH_OPENVDB_BLOSC=ON \
        -DWITH_SYSTEM_OPENJPEG=ON \
        -DWITH_MOD_OCEANSIM=ON \
        -DWITH_OPENIMAGEIO=ON \
        -DOPENIMAGEIO_ROOT_DIR=/usr \
        -DWITH_IMAGE_OPENEXR=ON \
        -DWITH_IMAGE_FRAMESERVER=OFF \
        -DWITH_CODEC_FFMPEG=ON \
        -DWITH_CODEC_SNDFILE=ON \
        -DWITH_OPENCOLLADA=ON \
        -DWITH_SDL=ON \
        -DWITH_LIBMV_SCHUR_SPECIALIZATIONS=ON \
        -DWITH_INPUT_NDOF=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_PYTHON_INSTALL=OFF \
        -DWITH_PYTHON_INSTALL_NUMPY=OFF \
        -DWITH_PYTHON_INSTALL_REQUESTS=OFF \
        -DCUDA_TOOLKIT_ROOT_DIR=/opt/cuda \
        -DCUDA_INCLUDE_DIRS=/opt/cuda/include \
        -DCUDA_CUDART_LIBRARY=/opt/cuda/lib64 \
        -DWITH_CYCLES_OSL=ON \
        -DWITH_CYCLES_CUDA_BINARIES=ON \
        -DCYCLES_CUDA_BINARIES_ARCH=sm_52 \
        -DWITH_CYCLES_LOGGING=OFF \
        -DWITH_LLVM=ON \
        -DLLVM_VERSION=3.5 \
        -DLLVM_STATIC=ON \
        -DWITH_DOC_MANPAGE=ON \
        -DWITH_C11=ON \
        -DWITH_CXX11=ON \
        ..
  make ${MAKEFLAGS}
}

package() {
  # Blender
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install

  # voir Extra
  python -m compileall "${pkgdir}/usr/share/blender"
  python -O -m compileall "${pkgdir}/usr/share/blender"
}
