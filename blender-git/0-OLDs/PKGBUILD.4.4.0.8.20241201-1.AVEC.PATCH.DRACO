# Maintainer: Name <name@fai.com>

pkgname=blender-git
pkgver=4.4.0.8.20241201
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64')
license=('GPL-3.0-or-later')
url="https://blender.org/"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'
          'libpng' 'openjpeg2' 'libtiff' 'openexr' 'libwebp' 'libjpeg-turbo'
          'python'
          'ffmpeg' 'fftw'
          'openal' 'freetype2' 'libsndfile' 'jack2' 'pipewire-pulse'
          'libglvnd' 'glfw' 'libxi'
          'boost-libs' 'onetbb' 'draco'
          'jemalloc'
          'python-numpy' 'python-psutil' 'python-requests' 'python-zstandard' 'pystring'
          'llvm-libs' 'clang'
          'potrace' 'pugixml' 'libharu' 'libepoxy'
          'libdecor' 'libxkbcommon'
          'fribidi' 'harfbuzz' 'minizip-ng' 'pybind11' 'shaderc'
          'vulkan-headers' 'vulkan-icd-loader' 'vulkan-driver'
          'opencolorio' 'libdeflate'
          'pcre' 'opencollada-git' 'openimageio-git'
          'openshadinglanguage-git'
          'opensubdiv-git' 'openvdb-git' 'alembic-git'
          'embree-git' 'openimagedenoise-git'
          'openpgl-git' 'materialx'
          'zstd'
)
makedepends=('git' 'git-lfs' 'cmake' 'boost' 'mesa' 'llvm'
             'cuda'
             'wayland-protocols'
             'mold'
)
optdepends=('cuda: cycles renderer cuda support')
conflicts=('blender')
provides=('blender')
#options=('!strip')
options=('!lto' '!debug')
source=("$pkgname-$pkgver.tar.zst"
        "OptiX-8.0-Include.zip"
        "force-draco1.patch"
        "force-draco2.patch"
        "ffmpeg-7-1.patch"
)
sha256sums=('be1885e3136d2cf3202174739e1b5332c142692634eb2692b74cc37da840ab09'
            'ba617fbb61587bac99106dbd6cda5a27c9d178308cc423878ed72b220b8b951c'
            '70464fb901abe81326e20c92f4dfef0d13bb8f20dcaee1903b292d0dc46bb091'
            'de4a5fb00721e9698fd22ec00d3781d1a25a884387820e8639f65f32308be86a'
            'af525f163bffab870b749b2cfd0d5a168224a3b8f9ace963a11dc34cd39dc1af'
)

_gitname="blender"

prepare() {
  cd "${_gitname}"

  # fix draco
  patch -p1 -i "$srcdir"/force-draco1.patch
  patch -p1 -i "$srcdir"/force-draco2.patch

  # fix build with ffmpeg 7
  patch -p1 -i "$srcdir"/ffmpeg-7-1.patch

  ## Cycles: Workaround for performance loss with the CUDA 9.0 SDK.
  ## Commit 1febc858559c054603073301a6c6dae44c737830 du 21/11/2017. Stefan Werner
  #sed -i -e "s|define CUDA_KERNEL_MAX_REGISTERS 48|define CUDA_KERNEL_MAX_REGISTERS 64|" intern/cycles/kernel/kernels/cuda/kernel_config.h

  # Workaround for python-numpy 2.0.0
  sed -i -e 's|numpy "core/include"|numpy "_core/include"|' CMakeLists.txt

  # Buildinfo : Workaround for HASH, BRANCH et COMMIT_TIMESTAMP
  sed -i -e 's|MY_WC_HASH "unknown"|MY_WC_HASH "74f212fae2c5"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_BRANCH "unknown"|MY_WC_BRANCH "main"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_COMMIT_TIMESTAMP 0|MY_WC_COMMIT_TIMESTAMP 1733035026|' build_files/cmake/buildinfo.cmake
}

build() {
  cd "${_gitname}"
  
  local _python_ver=3.12
  local _llvm_libs_ver=18

  cmake \
        -Bbuild \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_C_FLAGS_RELEASE:STRING=-DNDEBUG \
        -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
        -DCMAKE_SKIP_RPATH=ON \
        -DOPTIX_ROOT_DIR="${srcdir}" \
        -DWITH_XR_OPENXR=OFF \
        -DWITH_SYSTEM_FREETYPE=ON \
        -DWITH_LIBS_PRECOMPILED=OFF \
        -DWITH_USD=OFF \
        -DWITH_INPUT_NDOF=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_PYTHON_INSTALL=OFF \
        -DWITH_PYTHON_INSTALL_NUMPY=OFF \
        -DWITH_PYTHON_INSTALL_REQUESTS=OFF \
        -DWITH_PYTHON_INSTALL_ZSTANDARD=OFF \
        -DCUDA_HOST_COMPILER="$NVCC_CCBIN" \
        -DWITH_CYCLES_CUDA_BINARIES=ON \
        -DCYCLES_CUDA_BINARIES_ARCH="sm_86;compute_86" \
        -DWITH_CYCLES_NATIVE_ONLY=ON \
        -DWITH_CYCLES_DEVICE_HIP=OFF \
        -DWITH_CYCLES_DEVICE_METAL=OFF \
        -DWITH_LLVM=ON \
        -DLLVM_ROOT_DIR=/usr \
        -DLLVM_LIBPATH=/usr/lib \
        -DLLVM_LIBRARY=/usr/lib/libLLVM-${_llvm_libs_ver}.so \
        -DLLVM_INCLUDE_DIRS=/usr/include \
        -DWITH_CLANG=ON \
        -DCLANG_INCLUDE_DIR=/usr/include/clang \
        -D_clang_LIBRARIES=/usr/lib \
        -DWITH_LINKER_GOLD=OFF \
        -DWITH_LINKER_MOLD=ON \
        -DPYTHON_VERSION=${_python_ver} \
        -DPYTHON_LIBPATH=/usr/lib \
        -DPYTHON_LIBRARY=python${_python_ver} \
        -DPYTHON_INCLUDE_DIRS=/usr/include/python${_python_ver}
  make -C build
}

package() {
  # Blender
  cd "${_gitname}"
  DESTDIR="${pkgdir}" make -C build install

  # voir Community
  python -m compileall -d /usr/share/blender "${pkgdir}/usr/share/blender"
  python -O -m compileall -d /usr/share/blender "${pkgdir}/usr/share/blender"

  # Manually install draco bindings (See FS#73415)
  mkdir -p "${pkgdir}/usr/lib/python${_python_ver}"/
  mv "${pkgdir}"/usr/share/blender/4*/python/lib/* "${pkgdir}"/usr/lib/
  rm -r "${pkgdir}"/usr/share/blender/4*/python
  rm -r "${pkgdir}"/usr/lib/python
}
