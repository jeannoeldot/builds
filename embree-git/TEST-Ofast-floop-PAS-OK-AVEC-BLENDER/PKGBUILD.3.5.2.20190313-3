# Maintainer: Moi <name AT fai DOT fr>

pkgname=embree-git
pkgver=3.5.2.20190313
pkgrel=3
_ver_release="3.5.2"
_name_release="embree-${_ver_release}"
pkgdesc="High Performance Ray Tracing Kernels"
url="https://github.com/embree"
arch=('x86_64')
license=('Apache')
depends=('intel-tbb' 'glfw-x11'
)
makedepends=('cmake'
)
conflicts=('embree')
provides=('embree')
options=(staticlibs)
source=("embree-${_ver_release}.tar.gz"
        "enable-optimizations-Ofast-floop.patch"
)
sha256sums=('245af8820a820af94679fa1d43a05a9c825451be0d603b6165229556adf49517'
            '16c1b08427db83ac6d0d7d08d5663a90c747f59eb721fe1a7f74561eae1bc092'
)

prepare() {
  cd "${_name_release}"
  # Enable optimizations Ofast et floop
  patch -Np1 -i "${srcdir}/enable-optimizations-Ofast-floop.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DEMBREE_ISPC_SUPPORT=OFF \
    -DEMBREE_STATIC_LIB=ON \
    -DEMBREE_TUTORIALS=OFF \
    -DEMBREE_RAY_MASK=ON \
    -DEMBREE_TASKING_SYSTEM=INTERNAL \
    -DEMBREE_MAX_ISA=AVX2 \
    ..
  make VERBOSE=1
}

package() {
  cd "${_name_release}/build"
  
  make DESTDIR="${pkgdir}" install
}
