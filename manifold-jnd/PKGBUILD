# Maintainer: Moi <name AT fai DOT fr>

pkgname=manifold-jnd
pkgver=3.3.2.20251115
pkgrel=1
_ver_release="3.3.2"
_name_release="manifold-${_ver_release}"
pkgdesc="Geometry library dedicated to creating and operating on manifold triangle meshes"
url="https://github.com/elalish/manifold"
arch=('x86_64')
license=('Apache-2.0')
depends=('onetbb'
)
makedepends=('cmake' 'python'
)
conflicts=('manifold' 'manifold-git-jnd')
provides=('manifold')
#options=(staticlibs)
options=('!debug')
source=("manifold-${_ver_release}.tar.gz"
)
sha256sums=('92a37034c407156f71446f9ca03bd4487adeb1b8246a03d1c047b859b1b9d211'
)

build() {
  cd "${_name_release}"

  cmake \
    -Bbuild \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DMANIFOLD_CROSS_SECTION=OFF \
    -DMANIFOLD_DOWNLOADS=OFF \
    -DMANIFOLD_PAR=ON \
    -DMANIFOLD_TEST=OFF \
    -DMANIFOLD_CBIND=OFF \
    -DMANIFOLD_JSBIND=OFF
  make -C build
}

package() {
  cd "${_name_release}"
  
  DESTDIR="${pkgdir}" make -C build install
}
