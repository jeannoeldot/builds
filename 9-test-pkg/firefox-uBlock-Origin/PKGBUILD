# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

_pkgname=ublock_origin
_file=764482
pkgname=firefox-extension-${_pkgname}
pkgver=1.14.18
pkgrel=1
pkgdesc="An efficient blocker add-on. Fast, potent and lean."
license=('GPL')
arch=('any')
url="https://github.com/gorhill/uBlock#ublock-origin"
depends=("firefox")
makedepends=("unzip")
source=("${_pkgname}-${pkgver}.xpi::https://addons.mozilla.org/firefox/downloads/file/${_file}/${_pkgname}-${pkgver}.xpi"
)
noextract=("${_pkgname}-${pkgver}.xpi")
sha256sums=('18c7ec8129ef22a769a0442201b809846063febe5b18ae4b3ecf903f309f381a'
)

prepare() {
  cd "$srcdir"

  unzip -qqo "${_pkgname}-${pkgver}.xpi" -d "${_pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}"

  _extension_id="$(sed -n 's/.*"id": "\(.*\)".*/\1/p' ${_pkgname}-${pkgver}/manifest.json)"
  _extension_dest="${pkgdir}/usr/lib/firefox/browser/extensions/${_extension_id}"
#   # Not Unpacked
#   install -Dm644 ${_pkgname}-${pkgver}.xpi "${_extension_dest}.xpi"
  # Unpacked
  install -dm755 "${_extension_dest}"
  cp -R ${_pkgname}-${pkgver}/* "${_extension_dest}"
  chmod -R ugo+rX "${_extension_dest}"
}
