# Maintainer: Name <name@fai.com>

pkgname=opencolorio-git
pkgver=2.0.1.20210507
pkgrel=1
_name_release="OpenColorIO-2.0.1"
_ver_release="v2.0.1"
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
url="https://opencolorio.org"
license=('BSD')
depends=('freeglut' 'glew' 'lcms2' 'tinyxml' 'yaml-cpp' 'imath' 'pystring')
makedepends=('cmake' 'python' 'pybind11' 'ninja')
optdepends=('python: python bindings')
conflicts=('opencolorio' 'opencolorio2')
provides=('opencolorio')
source=(OpenColorIO-${_ver_release}.tar.gz
        opencolorio-openexr3.patch
)
sha512sums=('5787cc4cadb0a1832c925712ca0dfef6e843082b9461460e1798326c6fceaa97bee78e5298651e6a8a3662b348eab5ab9791e94c3efcac337da07229fb559338'
            '269a86e4020d272b64af631ba01f9b1e644bde119e9bfb1d5ec3fd62579d5d3f7ab31295fb6cffc51d7349c469e2ca6e12f69310317266eda602541e9fcff94f'
)

prepare() {
  patch -d ${_name_release} -p1 < opencolorio-openexr3.patch
}

build() {
  cd ${_name_release}

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja -C build
}

package() {
  cd ${_name_release}

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
