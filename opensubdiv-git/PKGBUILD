# Maintainer: Name <name@fai.com>

pkgname=opensubdiv-git
pkgver=3.4.4.20210216
pkgrel=2
_ver_release="3_4_4"
_name_release="OpenSubdiv-${_ver_release}"
pkgdesc="High performance subdivision surface (subdiv) evaluation on massively parallel CPU and GPU architectures"
arch=('x86_64')
license=('custom')
url="http://graphics.pixar.com/opensubdiv"
depends=('glfw-x11' 'tbb'
)
makedepends=('cmake' 'mesa')
conflicts=('opensubdiv')
provides=('opensubdiv')
source=("OpenSubdiv-v${_ver_release}.tar.gz"
        "opensubdiv-tbb2021.patch"
)
sha256sums=('20d49f80a2b778ad4d01f091ad88d8c2f91cf6c7363940c6213241ce6f1048fb'
            'ba5218bb42efb5ab11b2217742337825cb6369f760637e18787cf790c9a147ce'
)

prepare() {
  cd "${_name_release}"
  # Support TBB 2021.4
  # https://github.com/archlinux/svntogit-community/blob/packages/opensubdiv/trunk/PKGBUILD
  # https://github.com/PixarAnimationStudios/OpenSubdiv/commit/9079a517.patch
  patch -Np1 -i "${srcdir}/opensubdiv-tbb2021.patch"
}

build() {
  cd "${_name_release}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_SKIP_RPATH=ON \
        -D NO_PTEX=1 \
        -D NO_CLEW=1 \
        -D NO_CUDA=1 \
        -D NO_OPENCL=1 \
        -D NO_DOC=1 \
        -D NO_TUTORIALS=1 \
        -D NO_REGRESSION=1 \
        -D NO_EXAMPLES=1 \
        -D NO_METAL=1 \
        ..
  find . -type f -exec sed -i -e 's/\-O3//g' {} \;
  make
}

package() {
  cd "${_name_release}/build"
  make DESTDIR="${pkgdir}" install

  #license
  cd ..
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
