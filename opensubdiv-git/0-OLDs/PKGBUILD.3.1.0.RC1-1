# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=opensubdiv-git
pkgver=3.1.0.RC1
pkgrel=1
_osd_name="OpenSubdiv"
_osd_version="3_1_0_RC1"
pkgdesc="High performance subdivision surface (subdiv) evaluation on massively parallel CPU and GPU architectures"
arch=('i686' 'x86_64')
license=('custom')
url="http://graphics.pixar.com/opensubdiv"
depends=('glew' 'glfw-x11' 'intel-tbb'
)
makedepends=('cmake' 'mesa' 'cuda'
             'opencl-headers'
             'gcc5'
)
conflicts=('opensubdiv')
provides=('opensubdiv')
source=("https://github.com/PixarAnimationStudios/${_osd_name}/archive/v${_osd_version}.tar.gz"
)
sha256sums=('d0a1a860af9745fc8db05a41f32b1c0756066f11d8c553dcf64090ed549ad388'
)

build() {
  cd "${_osd_name}-${_osd_version}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -D CMAKE_C_COMPILER='/usr/bin/gcc-5' \
        -D CMAKE_CXX_COMPILER='/usr/bin/g++-5' \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_SKIP_RPATH=ON \
        -D NO_PTEX=1 \
        -D NO_CLEW=1 \
        -D NO_DOC=1 \
        -D NO_TUTORIALS=1 \
        -D NO_REGRESSION=1 \
        -D NO_EXAMPLES=1 \
        -D OSD_CUDA_NVCC_FLAGS="--gpu-architecture compute_52" \
        ..
  make ${MAKEFLAGS}
}

package() {
  cd "${_osd_name}-${_osd_version}/build"
  make DESTDIR="${pkgdir}" install

  # license
  cd ..
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
