# Maintainer: Name <name@fai.com>

pkgname=opensubdiv-git
pkgver=3.3.3.20180718
pkgrel=2
pkgdesc="High performance subdivision surface (subdiv) evaluation on massively parallel CPU and GPU architectures"
arch=('x86_64')
license=('custom')
url="http://graphics.pixar.com/opensubdiv"
depends=('glew' 'glfw-x11' 'intel-tbb'
)
makedepends=('cmake' 'mesa')
conflicts=('opensubdiv')
provides=('opensubdiv')
source=("$pkgname-$pkgver.tar.gz"
)
sha256sums=('e4aab03a2f5e1706f99c84f5f9339dae0153c277c2172a165dabee27e4be401d'
)

_gitname="opensubdiv"

build() {
  cd "${_gitname}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_SKIP_RPATH=ON \
        -D NO_PTEX=1 \
        -D NO_CLEW=1 \
        -D NO_CUDA=1 \
        -D NO_OPENCL=1 \
        -D NO_DOC=1 \
        -D NO_TUTORIALS=1 \
        -D NO_REGRESSION=1 \
        -D NO_EXAMPLES=1 \
        -D NO_METAL=1 \
        ..
  make ${MAKEFLAGS}
}

package() {
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install

  # license
  cd ..
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
