# Maintainer: Moi <name AT fai DOT fr>

pkgname=openvdb-git
pkgver=5.1.0.20180410
pkgrel=2
_ver_release="5.1.0"
_name_release="openvdb-${_ver_release}"
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib'
          'blosc' 'glfw-x11' 'glu'
          'jemalloc'
)
makedepends=('doxygen' 'boost' 'cmake' 'mesa'
)
conflicts=('openvdb')
provides=('openvdb')
source=("OpenVDB-v${_ver_release}.tar.gz"
        'boost-1.67.patch'
)
sha256sums=('eb5a8011732bcdeb115de9a38f640ee376bcb85b54e060d3b1ab08f9dc92f40b'
            '5c9b8a610bcfb1c005789cd29b7ca6c92da34c751ac9b23f8ff3e611c0822641'
)


prepare() {
  cd "${_name_release}"
  # Fix erreur : undefined reference to symbol 'pthread_condattr_setclock@@GLIBC_2.3.3'
  patch -Np1 -i "${srcdir}/boost-1.67.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DOPENVDB_BUILD_UNITTESTS=OFF \
    -DOPENVDB_BUILD_PYTHON_MODULE=OFF \
    -DOPENVDB_ENABLE_RPATH=OFF \
    -DOPENVDB_ENABLE_3_ABI_COMPATIBLE=OFF \
    -DBLOSC_LOCATION=/usr/ \
    -DTBB_LOCATION=/usr/ \
    -DUSE_GLFW3=ON \
    -DGLFW3_LOCATION=/usr/ \
    -DILMBASE_NAMESPACE_VERSIONING=OFF \
    -DOPENEXR_NAMESPACE_VERSIONING=OFF \
    -DILMBASE_LOCATION=/usr/ \
    -DOPENEXR_LOCATION=/usr/ \
    ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install
}
