# Maintainer: Name <name@fai.com>

pkgname=openvdb-git
pkgver=6.0.0.20181218
pkgrel=3
_ver_release="6.0.0"
_name_release="openvdb-${_ver_release}"
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib'
          'blosc' 'glfw-x11' 'glu'
          'jemalloc'
)
makedepends=('doxygen' 'boost' 'cmake' 'mesa'
)
conflicts=('openvdb')
provides=('openvdb')
source=("OpenVDB-v${_ver_release}.tar.gz"
        'fix-error-boost-egal-sup-1.67.patch'
)
sha256sums=('dbdf3048336444c402e5d3727c9bfb2e84454b8d0fd468ba92a8c7225e24b7b4'
            '7508921a498e26c17ad26896b65fec238c3cd5eeff8ab719b3ff167cc03ea726'
)


prepare() {
  cd "${_name_release}"
  # Fix erreur : undefined reference to symbol 'pthread_condattr_setclock@@GLIBC_2.3.3'
  patch -Np1 -i "${srcdir}/fix-error-boost-egal-sup-1.67.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DOPENVDB_BUILD_UNITTESTS=OFF \
    -DOPENVDB_BUILD_PYTHON_MODULE=OFF \
    -DOPENVDB_ENABLE_RPATH=OFF \
    -DOPENVDB_ENABLE_3_ABI_COMPATIBLE=OFF \
    -DBLOSC_LOCATION=/usr/ \
    -DTBB_LOCATION=/usr/ \
    -DUSE_GLFW3=ON \
    -DGLFW3_LOCATION=/usr/ \
    -DILMBASE_NAMESPACE_VERSIONING=OFF \
    -DOPENEXR_NAMESPACE_VERSIONING=OFF \
    -DILMBASE_LOCATION=/usr/ \
    -DOPENEXR_LOCATION=/usr/ \
    ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install
}
