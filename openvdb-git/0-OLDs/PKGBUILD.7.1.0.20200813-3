# Maintainer: Name <name@fai.com>

pkgname=openvdb-git
pkgver=7.1.0.20200813
pkgrel=3
_ver_release="7.1.0"
_name_release="openvdb-${_ver_release}"
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://www.openvdb.org/'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'tbb' 'zlib'
          'blosc' 'glfw-x11' 'glu'
          'jemalloc'
)
makedepends=('doxygen' 'boost' 'cmake' 'mesa'
)
conflicts=('openvdb')
provides=('openvdb')
source=("OpenVDB-v${_ver_release}.tar.gz"
        'fix-CMake-Error.patch'
)
sha256sums=('0c3588c1ca6e647610738654ec2c6aaf41a203fd797f609fbeab1c9f7c3dc116'
            'e52b345ee33558eb791d01f82974bdff4d5cde9b202bdbee0b80102dd74ceed8'
)

prepare() {
  cd "${_name_release}"
  # Fix CMake Error
  patch -Np1 -i "${srcdir}/fix-CMake-Error.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DOPENVDB_ENABLE_RPATH=OFF \
    -DUSE_EXR=ON \
    -DUSE_CCACHE=OFF \
    -DOPENVDB_CORE_STATIC=OFF \
    -DOPENVDB_BUILD_VDB_LOD=ON \
    -DOPENVDB_BUILD_VDB_RENDER=ON \
    -DOPENVDB_BUILD_VDB_VIEW=ON \
    ..

  sed -i "s/isystem/I/g" $(find . -name flags.make)
  
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install
}
