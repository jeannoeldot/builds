# Maintainer: Name <name@fai.com>

pkgname=openimageio-git
pkgver=2.2.7.0.20201001
pkgrel=2
_name_release="oiio-Release-2.2.7.0"
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('x86_64')
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'libtiff' 'opencolorio' 'tbb'
         'libjpeg-turbo' 'openjpeg2' 'libraw'
         'ffmpeg'
         'opencv'
         'qt5-base'
         'python'
         'libwebp'
         'freetype2'
         'fmt' 'pugixml' 'pybind11' 'python-numpy'
         'openvdb-git'
)
makedepends=('cmake' 'boost' 'mesa'
             'robin-map'
)
conflicts=('openimageio')
provides=('openimageio')
source=("$_name_release.tar.gz"
)
sha256sums=('857ac83798d6d2bda5d4d11a90618ff19486da2e5a4c4ff022c5976b5746fe8c'
)

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DVERBOSE=ON \
      -DOIIO_BUILD_TESTS=OFF \
      -DINSTALL_FONTS=OFF \
      -DSTOP_ON_WARNING=OFF \
      -DUSE_CCACHE=OFF \
      -DUSE_SIMD=avx2 \
      -DPYTHON_VERSION=3.8 \
      -DBUILD_MISSING_FMT=OFF \
      -DUSE_EXTERNAL_PUGIXML=ON \
      ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install

  # license
  install -Dm644 ../LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 ../THIRD-PARTY.md "${pkgdir}"/usr/share/licenses/${pkgname}/THIRD-PARTY
}
