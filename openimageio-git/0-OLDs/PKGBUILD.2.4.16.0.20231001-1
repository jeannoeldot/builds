# Maintainer: Name <name@fai.com>

pkgname=openimageio-git
pkgver=2.4.16.0.20231001
pkgrel=1
_name_release="OpenImageIO-2.4.16.0"
_ver_release="v2.4.16.0"
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('x86_64')
url="http://www.openimageio.org/"
license=('custom')
# TODO: Consider adding these deps: 'openvdb' 'opentbb' 'ptex' 'opencv'
depends=('openexr' 'boost-libs' 'libtiff'
         'libjpeg-turbo' 'openjpeg2' 'libraw' 'ffmpeg'
         'qt5-base'
         'python'
         'libwebp' 'libheif'
         'freetype2'
         'pugixml' 'pybind11' 'python-numpy'
         'opencolorio'
)
makedepends=('cmake' 'boost' 'mesa'
             'robin-map'
             'fmt'
)
conflicts=('openimageio')
provides=('openimageio')
source=("oiio-${_ver_release}.tar.gz"
)
sha256sums=('a8cea8b6087610e5154fbbe028548ab4fc773eb36531eef44ffb42351bc0b0f0'
)

build() {
  cd "${_name_release}"

  cmake \
      -Bbuild \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DOIIO_BUILD_TESTS=OFF \
      -DINSTALL_FONTS=OFF \
      -DUSE_CCACHE=OFF \
      -DUSE_SIMD=avx2 \
      -DPYTHON_VERSION=3.11 \
      -DOIIO_DOWNLOAD_MISSING_TESTDATA=OFF \
      -DUSE_EXTERNAL_PUGIXML=ON
  make -C build
}

package() {
  cd "${_name_release}"

  DESTDIR="${pkgdir}" make -C build install

  # license
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 THIRD-PARTY.md "${pkgdir}"/usr/share/licenses/${pkgname}/THIRD-PARTY
}
