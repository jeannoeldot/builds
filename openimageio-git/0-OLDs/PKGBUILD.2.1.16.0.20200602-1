# Maintainer: Name <name@fai.com>

pkgname=openimageio-git
pkgver=2.1.16.0.20200602
pkgrel=1
_name_release="oiio-Release-2.1.16.0"
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('x86_64')
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'libtiff' 'opencolorio' 'intel-tbb'
         'libjpeg-turbo' 'openjpeg2' 'libraw'
         'ffmpeg'
         'opencv'
         'qt5-base'
         'python'
         'libwebp'
         'freetype2'
         'openvdb-git'
)
makedepends=('cmake' 'boost' 'mesa' 'fmt'
             'robin-map-git'
             'pybind11' 'python-numpy'
)
conflicts=('openimageio')
provides=('openimageio')
source=("$_name_release.tar.gz"
)
sha256sums=('f44e3b3cffe9a8f47395da1ae59e972ecb26adf65f17581e6a489fdcce0cb116'
)

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DVERBOSE=ON \
      -DOIIO_BUILD_TESTS=OFF \
      -DINSTALL_FONTS=OFF \
      -DUSE_CCACHE=OFF \
      -DUSE_SIMD=avx2 \
      -DPYTHON_VERSION=3.8 \
      ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install

  # license
  install -Dm644 ../LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 ../LICENSE-THIRD-PARTY.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE-THIRD-PARTY
}
