# Maintainer: Name <name@fai.com>

pkgname=openimageio-git
pkgver=2.5.18.0.20250101
pkgrel=1
_name_release="OpenImageIO-2.5.18.0"
_ver_release="v2.5.18.0"
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('x86_64')
url="http://www.openimageio.org/"
license=('Apache-2.0')
# TODO: Consider adding these deps: 'openvdb' 'opentbb' 'ptex' 'opencv'
depends=('openexr' 'boost-libs' 'libtiff'
         'libjpeg-turbo' 'openjpeg2' 'libraw' 'ffmpeg'
         'qt6-base'
         'python'
         'libwebp' 'libheif'
         'freetype2'
         'pugixml' 'pybind11' 'python-numpy'
         'opencolorio'
)
makedepends=('cmake' 'boost' 'mesa'
             'robin-map'
             'fmt'
)
conflicts=('openimageio')
provides=('openimageio')
options=('!debug')
source=("OpenImageIO-${_ver_release}.tar.gz"
)
sha256sums=('f57481435cec18633d3eba9b2e8c483fc1df6f0a01c5c9f98cbae6d1c52928e5'
)

build() {
  cd "${_name_release}"

  cmake \
      -Bbuild \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DOIIO_BUILD_TESTS=OFF \
      -DINSTALL_FONTS=OFF \
      -DUSE_CCACHE=OFF \
      -DUSE_SIMD=avx2 \
      -DBUILD_MISSING_FMT=OFF \
      -DINTERNALIZE_FMT=OFF \
      -DPYTHON_VERSION=3.13 \
      -DOIIO_DOWNLOAD_MISSING_TESTDATA=OFF \
      -DUSE_EXTERNAL_PUGIXML=ON
  make -C build
}

package() {
  cd "${_name_release}"

  DESTDIR="${pkgdir}" make -C build install

  # license
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 THIRD-PARTY.md "${pkgdir}"/usr/share/licenses/${pkgname}/THIRD-PARTY
}
