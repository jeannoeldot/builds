# Maintainer: Name <name@fai.com>

pkgname=openimageio-jnd
pkgver=3.0.4.0.20250302
pkgrel=1
_name_release="OpenImageIO-3.0.4.0"
_ver_release="v3.0.4.0"
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('x86_64')
url="http://www.openimageio.org/"
license=('Apache-2.0')
# TODO: Consider adding these deps: 'openvdb' 'opentbb' 'ptex' 'opencv'
depends=('ffmpeg' 'freetype2'
         'giflib' 'libheif'
         'libraw' 'libtiff'
         'libpng' 'libwebp'
         'opencolorio' 'openexr'
         'libjpeg-turbo' 'openjpeg2' 'libjxl'
         'qt6-base'
         'python'
         'pugixml' 'pybind11' 'python-numpy'
         'fmt'
)
makedepends=('cmake' 'mesa'
             'robin-map'
)
conflicts=('openimageio' 'openimageio-git')
provides=('openimageio')
options=('!debug')
source=("OpenImageIO-${_ver_release}.tar.gz"
)
sha256sums=('dd481071d532c1ab38242011bb7af16f6ec7d915c58cada9fb78ed72b402ebc5'
)

build() {
  cd "${_name_release}"

  cmake \
      -Bbuild \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DOIIO_BUILD_TESTS=OFF \
      -DINSTALL_FONTS=OFF \
      -DUSE_CCACHE=OFF \
      -DUSE_EXTERNAL_PUGIXML=ON \
      -DUSE_SIMD=avx2 \
      -DOIIO_INTERNALIZE_FMT=OFF \
      -DPYTHON_VERSION=3.13
  make -C build
}

package() {
  cd "${_name_release}"

  DESTDIR="${pkgdir}" make -C build install

  # license
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 THIRD-PARTY.md "${pkgdir}"/usr/share/licenses/${pkgname}/THIRD-PARTY
}
