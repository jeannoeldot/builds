# Maintainer: Moi <name AT fai DOT fr>

pkgname=usd-git
pkgver=22.03.20220218
pkgrel=1
_ver_release="22.03"
_name_release="USD-${_ver_release}"
pkgdesc="Universal Scene Description"
url="http://www.openusd.org/"
arch=('x86_64')
license=('custom')
depends=('boost-libs' 'tbb2020'
)
makedepends=('boost' 'cmake' 'python'
)
conflicts=('usd')
provides=('usd')
source=("USD-v${_ver_release}.tar.gz"
        "blender-usd-diff.patch"
        "Fix-build-with-GCC11.patch"
)
sha256sums=('594910eab72168e47f48b5b02dbbdaf77084b1de717091b1104b6527e75d327a'
            '3a35ab44c7083f4d73a095bb12eae5c81098ad26123da899d2418329f48d7d31'
            '14c13162d29f5c0351abc337bc9e3290952b4fe75a46d028c1e0df38e57537a4'
)

prepare() {
  cd "${_name_release}"
  # blender/build_files/build_environment/patches/usd.diff
  patch -Np1 -i "${srcdir}/blender-usd-diff.patch"
  # Fix build with GCC 11
  patch -Np1 -i "${srcdir}/Fix-build-with-GCC11.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DTBB_INCLUDE_DIR=/usr/include/tbb2020 \
    -DTBB_LIBRARY=/usr/lib/tbb2020 \
    -DPXR_SET_INTERNAL_NAMESPACE=usdBlender \
    -DPXR_BUILD_TESTS=OFF \
    -DPXR_BUILD_EXAMPLES=OFF \
    -DPXR_BUILD_TUTORIALS=OFF \
    -DPXR_BUILD_USD_TOOLS=OFF \
    -DPXR_BUILD_IMAGING=OFF \
    -DPXR_ENABLE_PYTHON_SUPPORT=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DPXR_BUILD_MONOLITHIC=ON \
    -DCMAKE_DEBUG_POSTFIX=_d \
    ..
  find . -type f -exec sed -i -e 's/\-O3//g' {} \;
  make VERBOSE=1
}

package() {
  cd "${_name_release}/build"
  make DESTDIR="${pkgdir}" install
  
  # pxrConfig.cmake
  mkdir -p "${pkgdir}"/usr/lib/cmake/pxr
  mv "${pkgdir}"/usr/pxrConfig.cmake "${pkgdir}"/usr/lib/cmake/pxr
  # plugin
  mkdir -p "${pkgdir}"/usr/share/usd
  mv "${pkgdir}"/usr/plugin "${pkgdir}"/usr/share/usd/plugin
  
  #license
  cd ..
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
