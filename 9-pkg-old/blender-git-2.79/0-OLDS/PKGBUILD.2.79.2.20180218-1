# Maintainer: Moi <name AT fai DOT fr>

pkgname=blender-git
pkgver=2.79.2.20180218
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64')
license=('GPL')
url="http://blender.org/"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'
          'libjpeg' 'libpng' 'openjpeg' 'libtiff' 'openexr' 
          'python'
          'libsamplerate' 'ffmpeg' 'fftw'
          'openal' 'freetype2' 'libsndfile'
          'sdl2'
          'glew' 'glfw-x11'
          'libxi' 'libxmu' 'libxxf86vm'
          'boost-libs'
          'jemalloc'
          'opencolorio'
          'python-numpy'
          'opencollada-git' 'openshadinglanguage-git' 'openimageio-git'
          'opensubdiv-git'
          'openvdb-git'
          'llvm-libs'
)
makedepends=('cmake' 'boost' 'mesa' 'llvm'
             'cuda'
             'git'
)
optdepends=('cuda: cycles renderer cuda support')
conflicts=('blender')
provides=('blender')
options=('!strip')
install="blender.install"
source=("$pkgname-$pkgver.tar.gz"
        'fix-IndentationError-np_station.patch'
)
sha256sums=('5826c8865a1ac33b8d9ea58253fb3b01be1868c36e6d93c9bf6b6a38db8db187'
            'a3c09dd90f80536ff73dc4d216b47ace735ecdd8c88ec7858bdb4edb7b584873'
)

_gitname="blender"
_slot="2.79"

prepare() {
  cd "${_gitname}"
  # fix erreur : IndentationError: expected an indented block (np_point_dimension.py, line 148)
  patch -Np1 -i "${srcdir}/fix-IndentationError-np_station.patch"
  
  # fix erreur : Referenced library 'python3.5' is an uninstalled dependency
  sed -i -e "s/python3.5/python/" release/scripts/addons/io_blend_utils/install_whl.py

  # Cycles: Workaround for performance loss with the CUDA 9.0 SDK.
  # Commit 1febc858559c054603073301a6c6dae44c737830 du 21/11/2017. Stefan Werner
  sed -i "s|define CUDA_KERNEL_MAX_REGISTERS 48|define CUDA_KERNEL_MAX_REGISTERS 64|" intern/cycles/kernel/kernels/cuda/kernel_config.h
}

build() {
  cd "${_gitname}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_BUILDINFO=ON \
        -DWITH_FFTW3=ON \
        -DWITH_GAMEENGINE=OFF \
        -DWITH_OPENCOLORIO=ON \
        -DWITH_OPENSUBDIV=ON \
        -DWITH_OPENVDB=ON \
        -DWITH_OPENVDB_BLOSC=ON \
        -DWITH_SYSTEM_OPENJPEG=ON \
        -DWITH_MOD_OCEANSIM=ON \
        -DWITH_OPENIMAGEIO=ON \
        -DOPENIMAGEIO_ROOT_DIR=/usr \
        -DWITH_IMAGE_OPENEXR=ON \
        -DWITH_IMAGE_FRAMESERVER=OFF \
        -DWITH_CODEC_FFMPEG=ON \
        -DWITH_CODEC_SNDFILE=ON \
        -DWITH_OPENCOLLADA=ON \
        -DWITH_SDL=ON \
        -DWITH_INPUT_NDOF=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_PYTHON_INSTALL=OFF \
        -DWITH_PYTHON_INSTALL_NUMPY=OFF \
        -DWITH_PYTHON_INSTALL_REQUESTS=OFF \
        -DWITH_CYCLES_OSL=ON \
        -DWITH_CYCLES_OPENSUBDIV=ON \
        -DWITH_CYCLES_CUDA_BINARIES=ON \
        -DCYCLES_CUDA_BINARIES_ARCH=sm_52 \
        -DWITH_CYCLES_LOGGING=OFF \
        -DWITH_LLVM=ON \
        -DLLVM_VERSION=5.0 \
        -DLLVM_STATIC=OFF \
        -DWITH_DOC_MANPAGE=ON \
        -DWITH_C11=ON \
        -DWITH_CXX11=ON \
        -DPYTHON_VERSION=3.6 \
        -DPYTHON_LIBPATH=/usr/lib \
        -DPYTHON_LIBRARY=python3.6m \
        -DPYTHON_INCLUDE_DIRS=/usr/include/python3.6m \
        ..
  make ${MAKEFLAGS}
}

package() {
  # Blender
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install

  # Install addons_contrib. Version 2.79 du 05/08/2017 : NON installé par défaut
  msg "Install addons_contrib..."
  install -dm755 "${pkgdir}"/usr/share/blender/${_slot}/scripts/addons_contrib
  cp --preserve=mode -r ../release/scripts/addons_contrib/* \
                        "${pkgdir}"/usr/share/blender/${_slot}/scripts/addons_contrib/
  rm -f "${pkgdir}"/usr/share/blender/${_slot}/scripts/addons_contrib/{.arcconfig,.gitignore}

  # voir Community
  python -m compileall "${pkgdir}/usr/share/blender"
  python -O -m compileall "${pkgdir}/usr/share/blender"
}
