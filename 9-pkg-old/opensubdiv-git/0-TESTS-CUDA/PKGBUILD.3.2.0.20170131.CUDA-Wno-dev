# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=opensubdiv-git
pkgver=3.2.0.20170131
pkgrel=1
pkgdesc="High performance subdivision surface (subdiv) evaluation on massively parallel CPU and GPU architectures"
arch=('i686' 'x86_64')
license=('custom')
url="http://graphics.pixar.com/opensubdiv"
depends=('glew' 'glfw-x11' 'intel-tbb'
)
makedepends=('cmake' 'mesa' 'cuda'
             'opencl-headers'
             'gcc5'
)
conflicts=('opensubdiv')
provides=('opensubdiv')
source=("$pkgname-$pkgver.tar.gz"
        'fix-Erreur-compil-cmake-nvcc.patch'
)
sha256sums=('e8754542c23199c2e9113b1fc28d1e3e87f7a102150e089d94d5c60340ea07e7'
            'e3b430df7f41b32c31430dc594a50d33a6f83ec2aafbb9b4b06d878341f72be9'
)

_gitname="opensubdiv"

prepare() {
  cd "${_gitname}"
  # Syntax Warning in cmake code at column 63
  # Argument not separated from preceding token by whitespace.
  # This warning is for project developers.  Use -Wno-dev to suppress it.
  patch -Np1 -i "${srcdir}/fix-Erreur-compil-cmake-nvcc.patch"
}

build() {
  cd "${_gitname}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake -Wno-dev \
        -D CMAKE_C_COMPILER='/usr/bin/gcc-5' \
        -D CMAKE_CXX_COMPILER='/usr/bin/g++-5' \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_SKIP_RPATH=ON \
        -D NO_PTEX=1 \
        -D NO_CLEW=1 \
        -D NO_DOC=1 \
        -D NO_TUTORIALS=1 \
        -D NO_REGRESSION=1 \
        -D NO_EXAMPLES=1 \
        -D OSD_CUDA_NVCC_FLAGS="--gpu-architecture compute_52" \
        ..
  make ${MAKEFLAGS}
}

package() {
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install

  # license
  cd ..
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
