# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=opensubdiv-git
pkgver=3.0.3
pkgrel=6
_osd_name="OpenSubdiv"
_osd_version="3_0_3"
pkgdesc="High performance subdivision surface (subdiv) evaluation on massively parallel CPU and GPU architectures"
arch=('i686' 'x86_64')
license=('custom')
url="http://graphics.pixar.com/opensubdiv"
depends=('glew' 'glfw' 'intel-tbb'
         'nvidia-libgl'
)
makedepends=('cmake' 'mesa' 'cuda'
             'opencl-headers'
)
provides=('opensubdiv')
conflicts=('opensubdiv')
source=("https://github.com/PixarAnimationStudios/${_osd_name}/archive/v${_osd_version}.tar.gz"
)
sha256sums=('9b68336087048381b47487cceb7ab756bb35c32ae712ebe4fd33835e13cf9f8a'
)

prepare() {
  cd "${srcdir}/${_osd_name}-${_osd_version}"
  
  # nvcc fatal   : Value 'compute_11' is not defined for option 'gpu-architecture'
  sed -i "s|compute_11|compute_52|g" opensubdiv/CMakeLists.txt
}

build() {
  cd "${srcdir}/${_osd_name}-${_osd_version}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -D CMAKE_BUILD_TYPE=Release \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_SKIP_RPATH=ON \
        -D NO_PTEX=1 \
        -D NO_CLEW=1 \
        -D NO_MAYA=1 \
        -D NO_DOC=1 \
        -D NO_TUTORIALS=1 \
        -D NO_REGRESSION=1 \
        -D NO_EXAMPLES=1 \
        ..
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_osd_name}-${_osd_version}/build"
  make DESTDIR="${pkgdir}" install

  # license
  cd ..
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
