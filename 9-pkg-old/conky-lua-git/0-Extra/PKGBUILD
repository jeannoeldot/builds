# $Id$
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=conky
pkgver=1.8.2git20111107
pkgrel=2
pkgdesc='Lightweight system monitor for X'
url='http://conky.sourceforge.net/'
license=('BSD' 'GPL')
arch=('i686' 'x86_64')
options=('!emptydirs')
makedepends=('pkg-config' 'docbook2x')
depends=('alsa-lib' 'libxml2' 'curl' 'wireless_tools' 'libxft' 'glib2' 'libxdamage' 'imlib2')
backup=('etc/conky/conky.conf' 'etc/conky/conky_no_x11.conf')
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('133fc6fdc725162af9a98c72c1b7349efc22447c')

# git clone git://git.omp.am/conky.git; cd conky; git archive --prefix=${pkgname}-${pkgver}/ origin/1.8.2 | xz > ../${pkgname}-${pkgver}.tar.xz

replaces=('torsmo')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sh autogen.sh

	CPPFLAGS="${CXXFLAGS}" LIBS="${LDFLAGS}" ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-ibm \
		--enable-curl \
		--enable-rss \
		--enable-weather-xoap \
		--enable-imlib2 \
		--disable-lua \
		--enable-wlan \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 extras/vim/syntax/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax/conkyrc.vim
	install -Dm644 extras/vim/ftdetect/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}
