# $Id: PKGBUILD 118899 2011-04-09 10:32:42Z bisson $
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: JND <jeannoeldot@club-internet.fr>

pkgname=conky-lua-nvidia-git
pkgver=20120129
pkgrel=2
pkgdesc='Lightweight system monitor for X, with lua and nvidia bindings enabled.'
url='http://conky.sourceforge.net/'
license=('BSD' 'GPL')
arch=('i686' 'x86_64')
options=('!emptydirs')
makedepends=('pkg-config' 'cmake' 'git' 'docbook2x' 'tolua++')
depends=('alsa-lib' 'libxml2' 'curl' 'wireless_tools' 'libxft' 'glib2' 'libxdamage' 'imlib2'
	 'lua' 'cairo'
	 'nvidia' 'libxnvctrl'
)

#backup=('etc/conky/conky.conf' 'etc/conky/conky_no_x11.conf')

source=("${pkgname}-${pkgver}.tar.gz"
)
md5sums=('bf7409f15fc15a4236c93daec54fbb03'
)

provides=('conky')
conflicts=('conky')

replaces=('torsmo')

_gitname="conky"

build() {
	cd "${srcdir}"

	rm -fr build; mkdir build; cd build

	cmake \
		-D CMAKE_BUILD_TYPE:STRING="Release" \
		-D MAINTAINER_MODE:BOOL=ON \
		-D BUILD_CURL:BOOL=ON \
		-D BUILD_IMLIB2:BOOL=ON \
		-D BUILD_MYSQL:BOOL=OFF \
		-D BUILD_RSS:BOOL=ON \
		-D BUILD_WEATHER_METAR:BOOL=ON \
		-D BUILD_WEATHER_XOAP:BOOL=ON \
		-D BUILD_WLAN:BOOL=ON \
		-D BUILD_LUA_CAIRO:BOOL=ON \
		-D BUILD_LUA_IMLIB2:BOOL=ON \
		-D BUILD_NVIDIA:BOOL=ON \
		-D CMAKE_INSTALL_PREFIX:PATH=/usr \
		../conky
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
	install -d "${pkgdir}/usr/share/licenses/${_gitname}"
	install -m644 ../conky/{COPYING,LICENSE}* "${pkgdir}/usr/share/licenses/${_gitname}"
	install -Dm644 ../conky/extras/vim/syntax/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax/conkyrc.vim
	install -Dm644 ../conky/extras/vim/ftdetect/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}
