# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=blender-git
pkgver=2.77.1.20160604
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('i686' 'x86_64')
license=('GPL')
url="http://blender.org/"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info' 'xdg-utils'
          'libjpeg' 'libpng' 'openjpeg' 'libtiff' 'openexr' 
          'python'
          'nvidia-libgl'
          'libsamplerate' 'ffmpeg' 'fftw' 'freeglut'
          'openal' 'freetype2' 'libsndfile'
          'sdl2'
          'glew'
          'libxi' 'libxmu'
          'boost-libs'
          'gettext'
          'jemalloc'
          'opencolorio'
          'opencollada-git' 'openshadinglanguage-git' 'openimageio-git'
          'opensubdiv-git'
          'openvdb-git'
)
makedepends=('cmake' 'boost' 'mesa' 'llvm35'
             'cuda'
             'opencl-headers'
             'git'
             'gcc5'
)
optdepends=('cuda: cycles renderer cuda support')
provides=('blender')
conflicts=('blender')
options=('!strip')
install="blender.install"
source=("$pkgname-$pkgver.tar.gz"
        'fix-isfinite-was-not-declared-in-this-scope.patch'
)
sha256sums=('b65ef962d65e64edc921efb8f7892aa5f68aef5829b0700b38a0b115265cf0da'
            'df973cdeda86fc41c3938058769a94bd9fbb726aa49824777bd5ba860850bdfb'
)

_gitname="blender"

prepare() {
  cd "${_gitname}"
  
  ## Ajouts optimisations pour GNUCC dans CMakeLists.txt
  ## Supprimer : GCC 5.1/5.2/5.3 et FAST => ERREUR : INTERNAL COMPILER ERROR
  #sed -i "s|-pipe -fPIC -funsigned-char -fno-strict-aliasing|-pipe -fPIC -funsigned-char -fno-strict-aliasing -floop-interchange -floop-strip-mine -floop-block|g" CMakeLists.txt
  
  # Revert commit 21fddf7d1c0653881773e44cea6e9d0804a08b31 du Mon, 16 May 2016 de Brecht Van Lommel
  # C99/C++11: replace deprecated finite() by isfinite().
  # rayobject_rtbuild.cpp : error : ‘isfinite’ was not declared in this scope
  patch -Np1 -i "${srcdir}/fix-isfinite-was-not-declared-in-this-scope.patch"
}

build() {
  cd "${_gitname}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -DCMAKE_C_COMPILER='/usr/bin/gcc-5' \
        -DCMAKE_CXX_COMPILER='/usr/bin/g++-5' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_BUILDINFO=ON \
        -DWITH_FFTW3=ON \
        -DWITH_GAMEENGINE=OFF \
        -DWITH_OPENCOLORIO=ON \
        -DWITH_OPENSUBDIV=ON \
        -DWITH_OPENVDB=ON \
        -DWITH_OPENVDB_BLOSC=ON \
        -DWITH_SYSTEM_OPENJPEG=ON \
        -DWITH_MOD_OCEANSIM=ON \
        -DWITH_OPENIMAGEIO=ON \
        -DOPENIMAGEIO_ROOT_DIR=/usr \
        -DWITH_IMAGE_OPENEXR=ON \
        -DWITH_IMAGE_FRAMESERVER=OFF \
        -DWITH_CODEC_FFMPEG=ON \
        -DWITH_CODEC_SNDFILE=ON \
        -DWITH_OPENCOLLADA=ON \
        -DWITH_SDL=ON \
        -DWITH_LIBMV_SCHUR_SPECIALIZATIONS=ON \
        -DWITH_INPUT_NDOF=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_PYTHON_INSTALL=OFF \
        -DWITH_PYTHON_INSTALL_NUMPY=OFF \
        -DWITH_PYTHON_INSTALL_REQUESTS=OFF \
        -DWITH_CYCLES_OSL=ON \
        -DWITH_CYCLES_CUDA_BINARIES=ON \
        -DCYCLES_CUDA_BINARIES_ARCH=sm_52 \
        -DWITH_CYCLES_LOGGING=OFF \
        -DWITH_LLVM=ON \
        -DLLVM_VERSION=3.5 \
        -DLLVM_STATIC=ON \
        -DWITH_DOC_MANPAGE=ON \
        -DWITH_C11=ON \
        -DWITH_CXX11=ON \
        ..
  make ${MAKEFLAGS}
}

package() {
  # Blender
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install

  # voir Extra
  python -m compileall "${pkgdir}/usr/share/blender"
  python -O -m compileall "${pkgdir}/usr/share/blender"
}
