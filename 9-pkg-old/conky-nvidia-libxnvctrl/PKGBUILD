# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Maintainer: JND <jeannoeldot at club-internet dot fr>

pkgname=libxnvctrl
pkgver=313.30
pkgrel=1
pkgdesc="NVIDIA Ctrl X library"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('GPL2')
makedepends=('libx11' 'libxext' 'xextproto')
source=("http://download.nvidia.com/XFree86/nvidia-settings/nvidia-settings-$pkgver.tar.bz2"
)
sha1sums=('adbee99af57611a69aa2aa777abca4ba8a3150c8'
)

build() {
  cd "nvidia-settings-$pkgver/src/libXNVCtrl"

  export CFLAGS="$CFLAGS -fPIC"

  make clean
  make ${MAKEFLAGS}
}

package() {
  cd "nvidia-settings-$pkgver/src/libXNVCtrl"

  # install libXNVCtrl and headers
  install -d -m 755 "$pkgdir"/usr/{include/NVCtrl,lib}
  install -m 644 nv_control.h NVCtrl{,Lib}.h "$pkgdir"/usr/include/NVCtrl/
  install -m 644 libXNVCtrl.a "$pkgdir"/usr/lib/libXNVCtrl.a
}

# vim:set ts=2 sw=2 et:
