# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=openshadinglanguage-git
pkgver=1.9.6.20180102
pkgrel=1
_name_release="OpenShadingLanguage-Release-1.9.6"
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'openimageio-git'
         'llvm-libs'
)
makedepends=('boost' 'cmake' 'llvm' 'clang')
conflicts=('openshadinglanguage')
provides=('openshadinglanguage')
source=("$_name_release.tar.gz"
)
sha256sums=('d8c75730a5ff1c7c0e6b6e14c75e15e64ade34b37c3910698d3ec50596897b5d'
)

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_SKIP_RPATH=ON \
      -DSTOP_ON_WARNING=OFF \
      -DLLVM_STATIC=OFF \
      -DOSL_BUILD_TESTS=OFF \
      -DUSE_PARTIO=OFF \
      -DUSE_SIMD=avx2 \
      -DUSE_CCACHE=OFF \
    ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install

  # Install lib*.so par d√©faut dans lib64 au lieu de lib
  mv -v "${pkgdir}/usr/lib64" "${pkgdir}/usr/lib"

  # license
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  mv -v "${pkgdir}"/usr/share/doc/OSL/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}

  # Ugly hacks /usr/shaders/*.oso and /usr/shaders/MaterialX/*.oso
  sed -i "s|/build/${pkgname}/src/${_name_release}/src/|/usr/|g" "${pkgdir}"/usr/shaders/*.oso
  sed -i "s|/build/${pkgname}/src/${_name_release}/src/|/usr/|g" "${pkgdir}"/usr/shaders/MaterialX/*.oso
}
