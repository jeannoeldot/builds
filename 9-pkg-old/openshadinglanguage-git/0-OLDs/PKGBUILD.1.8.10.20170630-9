# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=openshadinglanguage-git
pkgver=1.8.10.20170630
pkgrel=9
_name_release="OpenShadingLanguage-Release-1.8.10"
pkgdesc="Advanced shading language for production GI renderers"
arch=('i686' 'x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'openimageio-git'
         'llvm-libs'
)
makedepends=('boost' 'cmake' 'llvm' 'clang')
conflicts=('openshadinglanguage')
provides=('openshadinglanguage')
source=("$_name_release.tar.gz"
        'llvm50-Make-OSL-work-with-llvm5.patch'
)
sha256sums=('5b9ad6814fd55e68f4ac50e4214120629ab9175142dc93aac350d0f54bea745a'
            'd349622f1aaeb72336587296933adaba479e2f4b3045bfa47db98712522ae42e'
)

prepare() {
  cd "${_name_release}"
   
  # Make OSL work with clang5/llvm5 (#787)
  # Commit 104bd26e7153b0ae9e12fd1a98112e3697836f5d
  #  lgritz committed on GitHub on 18 Aug
  patch -Np1 -i "${srcdir}/llvm50-Make-OSL-work-with-llvm5.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DSTOP_ON_WARNING=OFF \
      -DLLVM_STATIC=OFF \
      -DOSL_BUILD_TESTS=OFF \
      -DUSE_PARTIO=OFF \
      -DUSE_SIMD=avx2 \
      -DUSE_CCACHE=OFF \
    ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}"/usr/share/OSL/
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/doc/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname} # TODO: Tell upstream about this shit
  mv "${pkgdir}"/usr/doc/{INSTALL.md,CHANGES.md,README.md} "${pkgdir}"/usr/share/OSL/
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/OSL/doc
#  mv "${pkgdir}"/usr/shaders "${pkgdir}"/usr/share/OSL/shaders
}
