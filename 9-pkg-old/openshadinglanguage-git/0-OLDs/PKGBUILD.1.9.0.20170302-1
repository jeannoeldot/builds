# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=openshadinglanguage-git
pkgver=1.9.0.20170302
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=('i686' 'x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'libwebp' 'llvm-libs'
         'openimageio-git'
)
makedepends=('boost' 'cmake' 'llvm' 'clang')
conflicts=('openshadinglanguage')
provides=('openshadinglanguage')
source=("$pkgname-$pkgver.tar.gz"
        'llvm39-static-system-libs.patch'
)
sha256sums=('da2c5763fa8196eec1cad8a1600080d2fae6ba73b0ac881c782b6203532a9b96'
            '4dd81b1652c2e306884fb3bd8a23f5e83303c115130fbeb67f5327da54e71eb2'
)

_gitname="osl"

prepare() {
  cd "${_gitname}"
   
  # Add system libraries needed to link against LLVM components (zlib, ncurses)
  # This is required in order to statically link against LLVM
  # VOIRÂ Community
  patch -Np1 -i "${srcdir}/llvm39-static-system-libs.patch"
}
  
build() {
  cd "${_gitname}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DSTOP_ON_WARNING=OFF \
      -DLLVM_STATIC=ON \
      -DOSL_BUILD_TESTS=OFF \
      -DUSE_PARTIO=OFF \
      -DUSE_SIMD=sse4.2 \
      -DUSE_CCACHE=OFF \
    ..
  make
}

package() {
  cd "${_gitname}/build"

  make DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}"/usr/share/OSL/
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname} # TODO: Tell upstream about this shit
  mv "${pkgdir}"/usr/{INSTALL.md,CHANGES.md,README.md} "${pkgdir}"/usr/share/OSL/
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/OSL/doc
# !!! TESTS
#  mv "${pkgdir}"/usr/shaders "${pkgdir}"/usr/share/OSL/shaders
}
