# Maintainer: JND <jeannoeldot@club-internet.fr>
pkgname=blender-svn
pkgver=2.61.0.42683
pkgrel=2
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('i686' 'x86_64')
url="http://blender.org/"
depends=('libjpeg' 'libpng' 'openjpeg' 'libtiff' 'openexr' 
	 'libgl' 'mesa' 'python'
	 'libsamplerate' 'ffmpeg' 'fftw' 'freeglut'
         'openal' 'freetype2' 'sdl' 'libsndfile' 'jack'
         'eigen' 'glew'
	 'libxi' 'libxmu'
	 'gettext' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info' 'xdg-utils'
	 'openimageio-git' 'boost-libs'
	 'opencollada-svn'
)
optdepends=('cuda-toolkit: CUDA support in Cycles')
makedepends=('yasm' 'cmake' 'boost' 'swig' 'python')
provides=('blender')
license=('GPL')
install=blender.install
source=($pkgname-$pkgver.tar.gz
)
md5sums=('1fb90eb2387ec19ee93a20724365665c'
)

_svnmod="blender"
_slot="2.61"

_yafarayname="YafaRay"

build() {
  cd ${srcdir}/${_svnmod}

  # compile blender
  mkdir ${srcdir}/${_svnmod}-build
  cd ${srcdir}/${_svnmod}-build
  cmake	-DCMAKE_BUILD_TYPE=RELEASE \
	-DCMAKE_SKIP_RPATH=ON \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DWITH_FFTW3=ON \
	-DWITH_GAMEENGINE=OFF \
	-DWITH_BUILTIN_GLEW=OFF \
	-DWITH_MOD_CLOTH_ELTOPO=ON \
	-DWITH_MOD_OCEANSIM=ON \
	-DWITH_CODEC_FFMPEG=ON \
	-DWITH_CODEC_SNDFILE=ON \
	-DWITH_OPENCOLLADA=ON \
	-DWITH_JACK=ON \
	-DWITH_INPUT_NDOF=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
	-DWITH_PYTHON_INSTALL=OFF \
	-DPYTHON_VERSION=3.2 \
	-DPYTHON_LIBPATH=/usr/lib \
	-DPYTHON_LIBRARY=python3.2mu \
	-DPYTHON_INCLUDE_DIRS=/usr/include/python3.2mu \
	${srcdir}/${_svnmod}
  make ${MAKEFLAGS}

  # now we will compile some plugins
  cp -rf ${srcdir}/${_svnmod}/release/plugins/* \
    ${srcdir}/${_svnmod}/source/blender/blenpluginapi/
  cd ${srcdir}/${_svnmod}/source/blender/blenpluginapi
  sed -i "s|-fPIC -funsigned-char -O2|-march=native -O2 -pipe -fPIC -funsigned-char|g" bmake
  chmod 755 bmake
  make ${MAKEFLAGS}

  # compile YafaRay
  cd ${srcdir}/${_svnmod}/${_yafarayname}

  cp CMakeConfig/UserConfig.template CMakeConfig/UserConfig.txt
  sed -i "s|set(WITH_QT ON)|set(WITH_QT OFF)|" CMakeConfig/UserConfig.txt
  sed -i "s|set(BUILDRELEASE OFF)|set(BUILDRELEASE ON)|" CMakeConfig/UserConfig.txt
  sed -i "s|#set(YAF_LIB_DIR lib)|set(YAF_LIB_DIR bin)|" CMakeConfig/UserConfig.txt
  sed -i "s|#set(USER_RELEASE_EXTRA_FLAGS \"\")|set(USER_RELEASE_EXTRA_FLAGS \"-march=native -pipe -fstack-protector --param=ssp-buffer-size=4 -D_FORTIFY_SOURCE=2 -fPIC -funsigned-char -fno-strict-aliasing -floop-interchange -floop-strip-mine -floop-block\")|" CMakeConfig/UserConfig.txt
  sed -i "s|#set(YAF_PY_VERSION 3.1)|set(YAF_PY_VERSION 3.2)|" CMakeConfig/UserConfig.txt

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr/share/blender/${_slot}/scripts/addons/yafaray \
    -DYAF_BINDINGS_PY_DIR=/usr/share/blender/${_slot}/scripts/addons/yafaray/bin \
    -DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'
  make ${MAKEFLAGS}

}

package() {
  # Blender
  cd ${srcdir}/${_svnmod}-build
  make DESTDIR=${pkgdir} install

  # YafaRay
  cd ${srcdir}/${_svnmod}/${_yafarayname}/build
  make DESTDIR=${pkgdir} install

  mv ${pkgdir}/usr/share/blender/${_slot}/scripts/addons/yafaray/bin/yafaray \
    ${pkgdir}/usr/share/blender/${_slot}/scripts/addons/yafaray/bin/plugins

  cd ${srcdir}/${_svnmod}/${_yafarayname}
  cp -a -R Blender-2.5-Exporter/* ${pkgdir}/usr/share/blender/${_slot}/scripts/addons/yafaray/

  # voir Extra
  python -m compileall "${pkgdir}/usr/share/blender"

  # install Blender plugins
  install -d -m755 ${pkgdir}/usr/share/blender/${_slot}/plugins/{sequence,texture}
  cp ${srcdir}/${_svnmod}/source/blender/blenpluginapi/sequence/*.so \
     ${pkgdir}/usr/share/blender/${_slot}/plugins/sequence/
  cp ${srcdir}/${_svnmod}/source/blender/blenpluginapi/texture/*.so \
     ${pkgdir}/usr/share/blender/${_slot}/plugins/texture/

  # FIX: making all python scripts readable only by group 'users',
  #      so nobody can modify scripts apart root user, but python
  #      cache (*.pyc) can be written and shared across the users.
  #chown root:users -R "${D}/usr/share/${PN}/${SLOT}/scripts"
  #chmod 750 -R "${D}/usr/share/${PN}/${SLOT}/scripts"

}
