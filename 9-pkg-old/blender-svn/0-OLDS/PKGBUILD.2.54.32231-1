# Maintainer: JND <jeannoeldot@club-internet.fr>
pkgname=blender25-svn
pkgver=2.54.32231
pkgrel=2
pkgdesc="SVN version of Blender 2.5 (called bf-blender)"
arch=('i686' 'x86_64')
url="http://blender.org/"
depends=('libpng' 'libgl' 'mesa' 'openexr' 'python3' 'libsamplerate' 'ffmpeg' 'fftw' 'lcms'
         'openal' 'freetype2' 'sdl'
         'eigen' 'glew'
	 'gettext' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('python' 'subversion' 'yasm' 'cmake')
provides=('blender25')
#conflicts=('blender')
license=('GPL')
install=blender25.install
source=($pkgname-$pkgver.tar.gz
	blender25-wrapper
)
md5sums=('365acfb3ac2ba827a1fd9567cfb6f37a'
	 '8a58a748c0117ce76215a7d0fd23f04c'
)

_svnmod="blender"
_slot="2.5"

build() {
  cd $srcdir/$_svnmod

  mkdir $srcdir/$_svnmod-build
  cd $srcdir/$_svnmod-build
  # compile
  cmake	-DCMAKE_BUILD_TYPE=RELEASE \
	-DCMAKE_SKIP_RPATH=ON \
	-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DWITH_LCMS=ON \
	-DWITH_FFTW3=ON \
	-DWITH_FFMPEG=ON \
	-DWITH_GAMEENGINE=OFF \
	-DWITH_PYTHON_INSTALL=OFF \
	$srcdir/$_svnmod
  make

  # now we will compile some plugins
  cp -rf $srcdir/$_svnmod/release/plugins/* \
    $srcdir/$_svnmod/source/blender/blenpluginapi/
  cd $srcdir/$_svnmod/source/blender/blenpluginapi
  sed -i "s|-fPIC -funsigned-char -O2|-march=native -O2 -pipe -fPIC -funsigned-char|g" bmake
  chmod 755 bmake
  make
}

package() {
  cd $srcdir/$_svnmod-build
  make DESTDIR=$pkgdir install

  cd $srcdir
  # install binaries
  mv $pkgdir/usr/bin/blender $pkgdir/usr/bin/$pkgname-bin
  install -D -m755 $srcdir/blender25-wrapper $pkgdir/usr/bin/$pkgname

  # install plugins
  install -d -m755 $pkgdir/usr/share/blender/$_slot/plugins/{sequence,texture}
  cp $srcdir/$_svnmod/source/blender/blenpluginapi/sequence/*.so \
     $pkgdir/usr/share/blender/$_slot/plugins/sequence/
  cp $srcdir/$_svnmod/source/blender/blenpluginapi/texture/*.so \
     $pkgdir/usr/share/blender/$_slot/plugins/texture/

  # install desktop file and pixmap
  mv $pkgdir/usr/share/applications/$_svnmod.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  mv $pkgdir/usr/share/pixmaps/$_svnmod.svg $pkgdir/usr/share/pixmaps/$pkgname.svg

  sed -i "s|Name=Blender|Name=Blender25-svn|g" $pkgdir/usr/share/applications/$pkgname.desktop
  sed -i "s|Exec=blender|Exec=blender25-svn|g" $pkgdir/usr/share/applications/$pkgname.desktop
  sed -i "s|Icon=blender.svg|Icon=blender25-svn.svg|g" $pkgdir/usr/share/applications/$pkgname.desktop
  sed -i "s|modeleur|Modeleur|g" $pkgdir/usr/share/applications/$pkgname.desktop

  # install scripts and datafiles in $pkgdir/usr/share/blender/$_slot/
  mv $pkgdir/usr/share/blender/2.54/* $pkgdir/usr/share/blender/$_slot/
  rm -r $pkgdir/usr/share/blender/2.54

  # install VERSION
  install -D -m644 $srcdir/$_svnmod/release/VERSION \
		   $pkgdir/usr/share/blender/$_slot/VERSION

  # install docs

  # FIX: making all python scripts readable only by group 'users',
  #      so nobody can modify scripts apart root user, but python
  #      cache (*.pyc) can be written and shared across the users.
  #chown root:users -R "${D}/usr/share/${PN}/${SLOT}/scripts"
  #chmod 750 -R "${D}/usr/share/${PN}/${SLOT}/scripts"

}
