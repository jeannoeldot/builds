# Maintainer: JND <jeannoeldot@club-internet.fr>
pkgname=blender-svn
pkgver=2.5.29731
pkgrel=1
pkgdesc="SVN version of Blender 2.5 (called bf-blender)"
arch=('i686' 'x86_64')
url="http://blender.org/"
depends=('libpng' 'libgl' 'mesa' 'openexr' 'python3' 'libsamplerate' 'ffmpeg' 'fftw' 'lcms'
         'openal' 'freetype2'
         'eigen' 'glew'
	 'gettext' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('scons' 'libxi')
provides=('blender')
conflicts=('blender')
license=('GPL')
install=blender.install
source=($pkgname-$pkgver.tar.gz
	blender.config
	blender-2.5-use_usr.diff
	blender-2.5-doxygen.patch
	blender-2.5-eigen.patch
	blender-2.5-glew.patch
)
md5sums=('a0699e2af72f887456cd166da89fd79c'
'618c71f5fc05fdd8701ebda4b11b00dd'
'8de5594267ff41363cb557103ea70f2a'
'078fe540ef2c2652a955624a84649bae'
'95551e0fc553f09ea7543fff94e657d2'
'dc0469f7d629395eb6f7c8e0bb0f79fc'
)

_svnmod="blender"

build() {
  cd $srcdir/$_svnmod

  cp $srcdir/blender.config user-config.py

	# Voir Gentoo
	patch -Np0 < $srcdir/blender-2.5-doxygen.patch

	# OpenJPEG
	msg "Removing bundled OpenJPEG ..."
	rm -rf extern/libopenjpeg

	# Glew
	msg "Removing bundled Glew ..."
	rm -rf extern/glew
	patch -Np0 < $srcdir/blender-2.5-glew.patch

	# Eigen2
	msg "Removing bundled Eigen2 ..."
	rm -rf extern/Eigen2
	patch -Np0 < $srcdir/blender-2.5-eigen.patch

  # correct the BLENDERPATH variable
  # see http://wiki.blender.org/index.php/Dev:2.5/Source/Installation/Unix_FHS
  patch -Np0 < $srcdir/blender-2.5-use_usr.diff

  # compile
  scons

  # now we will compile some plugins
  mv -f $srcdir/install/linux2/share/blender/*/plugins/* \
    $srcdir/$_svnmod/source/blender/blenpluginapi/
  cd $srcdir/$_svnmod/source/blender/blenpluginapi
  sed -i "s|-fPIC -funsigned-char -O2|-march=native -O2 -pipe -fPIC -funsigned-char|g" bmake
  chmod 755 bmake
  make
}

package() {
  cd $srcdir

  # install binaries
  install -d -m755 $pkgdir/usr
  cp -a $srcdir/install/linux2/* $pkgdir/usr/

  # install plugins
  install -d -m755 $pkgdir/usr/share/blender/2.5/plugins/{sequence,texture}
  cp $srcdir/$_svnmod/source/blender/blenpluginapi/sequence/*.so \
     $pkgdir/usr/share/blender/2.5/plugins/sequence/
  cp $srcdir/$_svnmod/source/blender/blenpluginapi/texture/*.so \
     $pkgdir/usr/share/blender/2.5/plugins/texture/


  # install desktop file
  install -D -m644 $srcdir/$_svnmod/release/freedesktop/icons/scalable/$_svnmod.svg \
		   $pkgdir/usr/share/pixmaps/$_svnmod.svg

  sed -i "s|Name=Blender|Name=Blender 2.5|g" $_svnmod/release/freedesktop/$_svnmod.desktop
  sed -i "s|blender.png|blender.svg|g" $_svnmod/release/freedesktop/$_svnmod.desktop

  install -D -m644 $srcdir/$_svnmod/release/freedesktop/$_svnmod.desktop \
		   $pkgdir/usr/share/applications/$_svnmod.desktop

  # install docs

  # FIX: making all python scripts readable only by group 'users',
  #      so nobody can modify scripts apart root user, but python
  #      cache (*.pyc) can be written and shared across the users.
  #chown root:users -R "${D}/usr/share/${PN}/${SLOT}/scripts"
  #chmod 750 -R "${D}/usr/share/${PN}/${SLOT}/scripts"

}
