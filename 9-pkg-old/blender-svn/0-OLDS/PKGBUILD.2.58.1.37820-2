# Maintainer: JND <jeannoeldot@club-internet.fr>
pkgname=blender25-svn
pkgver=2.58.1.37820
pkgrel=2
pkgdesc="SVN version of Blender 2.5 (called bf-blender)"
arch=('i686' 'x86_64')
url="http://blender.org/"
depends=('libjpeg' 'libpng' 'openjpeg' 'libtiff' 'openexr' 
	 'libgl' 'mesa' 'python'
	 'libsamplerate' 'ffmpeg' 'fftw'
         'openal' 'freetype2' 'sdl' 'libsndfile'
         'eigen' 'glew'
	 'libxi' 'libxmu'
	 'gettext' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info' 'xdg-utils')
makedepends=('yasm' 'cmake')
provides=('blender25')
license=('GPL')
install=blender25.install
source=($pkgname-$pkgver.tar.gz
)
md5sums=('0cff9aca459cd0a8c410206d6688545c'
)

_svnmod="blender"
_slot="2.58"

build() {
  cd $srcdir/$_svnmod

  mkdir $srcdir/$_svnmod-build
  cd $srcdir/$_svnmod-build
  # compile
  cmake	-DCMAKE_BUILD_TYPE=RELEASE \
	-DCMAKE_SKIP_RPATH=ON \
	-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DWITH_BUILTIN_GLEW=OFF \
	-DWITH_FFTW3=ON \
	-DWITH_CODEC_FFMPEG=ON \
	-DWITH_CODEC_SNDFILE=ON \
	-DWITH_GAMEENGINE=OFF \
	-DWITH_PYTHON_INSTALL=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
	-DPYTHON_VERSION=3.2 \
	-DPYTHON_LIBPATH=/usr/lib \
	-DPYTHON_LIBRARY=python3.2mu \
	-DPYTHON_INCLUDE_DIRS=/usr/include/python3.2mu \
	$srcdir/$_svnmod
  make $MAKEFLAGS

  # now we will compile some plugins
  cp -rf $srcdir/$_svnmod/release/plugins/* \
    $srcdir/$_svnmod/source/blender/blenpluginapi/
  cd $srcdir/$_svnmod/source/blender/blenpluginapi
  sed -i "s|-fPIC -funsigned-char -O2|-march=native -O2 -pipe -fPIC -funsigned-char|g" bmake
  chmod 755 bmake
  make $MAKEFLAGS
}

package() {
  cd $srcdir/$_svnmod-build
  make DESTDIR=$pkgdir install
# voir Extra
  python -m compileall "${pkgdir}/usr/share/blender"

  cd $srcdir
  # install binaries
  mv $pkgdir/usr/bin/blender $pkgdir/usr/bin/$pkgname-bin

  # install plugins
  install -d -m755 $pkgdir/usr/share/blender/$_slot/plugins/{sequence,texture}
  cp $srcdir/$_svnmod/source/blender/blenpluginapi/sequence/*.so \
     $pkgdir/usr/share/blender/$_slot/plugins/sequence/
  cp $srcdir/$_svnmod/source/blender/blenpluginapi/texture/*.so \
     $pkgdir/usr/share/blender/$_slot/plugins/texture/

  # install desktop file and pixmap
  mv $pkgdir/usr/share/applications/$_svnmod.desktop $pkgdir/usr/share/applications/$pkgname.desktop
#  mv $pkgdir/usr/share/pixmaps/$_svnmod.svg $pkgdir/usr/share/pixmaps/$pkgname.svg

  sed -i "s|Name=Blender|Name=Blender25-svn|g" $pkgdir/usr/share/applications/$pkgname.desktop
  sed -i "s|Exec=blender|Exec=blender25-svn-bin|g" $pkgdir/usr/share/applications/$pkgname.desktop
#  sed -i "s|Icon=blender.svg|Icon=blender25-svn.svg|g" $pkgdir/usr/share/applications/$pkgname.desktop
  sed -i "s|modeleur|Modeleur|g" $pkgdir/usr/share/applications/$pkgname.desktop


  # FIX: making all python scripts readable only by group 'users',
  #      so nobody can modify scripts apart root user, but python
  #      cache (*.pyc) can be written and shared across the users.
  #chown root:users -R "${D}/usr/share/${PN}/${SLOT}/scripts"
  #chmod 750 -R "${D}/usr/share/${PN}/${SLOT}/scripts"

}
