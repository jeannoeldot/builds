# Maintainer: JND

pkgname=bluegriffon-svn
pkgver=0.9.588
pkgrel=3
_bgname=bluegriffon
_bgver=0.9.1
_ffver=4.0rc1
_mozver=2.0
pkgdesc="New WYSIWYG content editor for the Web powered by Gecko (XULRunner independent)"
url="http://bluegriffon.org/"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')

makedepends=('autoconf2.13' 'gcc' 'zip' 'unzip' 'pkgconfig' 'diffutils'
             'libgnomeui' 'python2' 'wireless_tools' 'yasm'
             'xorg-server-xvfb' 'mesa'
)
depends=('gtk2' 'gcc-libs' 'libidl2' 'mozilla-common' 'hicolor-icon-theme' 'gnome-vfs'
         'nss' 'nspr' 'libxt' 'hunspell' 'startup-notification'
         'libnotify' 'mime-types' 'dbus-glib' 'desktop-file-utils'
         'libpng' 'libevent' 'alsa-lib' 'mesa'
         'cairo-tee' 'shared-mime-info' 'lcms' 'libvpx'
)
#optdepends=('libgnomeui: GNOME integration and MIME handling'
#            'wireless_tools: Location aware browsing'
#)

provides=("bluegriffon")
conflicts=("bluegriffon")

install=bluegriffon.install

#"ftp://ftp.mozilla.org/pub/mozilla.org/firefox/nightly/${_ffver}-candidates/${_build}/source/firefox-${_ffver}.source.tar.bz2"

source=("ftp://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${_ffver}/source/firefox-${_ffver}.source.tar.bz2"
	"$pkgname-$pkgver.tar.bz2"
        'mozconfig'
        'bluegriffon.desktop'
)
md5sums=('511828dcc226f38602c6c67bd192ef40'
         '08082131d10aae79edef5223a5cd3aeb'
         '4437e457b21e783580313dff9af7c272'
         '78b1613eea6d2cc161df6602328b88c4'
)


build() {
	cd "$srcdir"/mozilla-$_mozver

	tar xjf "$srcdir/$pkgname-$pkgver.tar.bz2"

#	voir http://bluegriffon.org/pages/Build-BlueGriffon
	patch -p 1 < bluegriffon/config/content.patch

	cp "$srcdir"/mozconfig .mozconfig

	export CFLAGS="-march=native -O2 -pipe -fomit-frame-pointer -floop-interchange -floop-strip-mine -floop-block"
	export CXXFLAGS="-march=native -O2 -pipe -fomit-frame-pointer -floop-interchange -floop-strip-mine -floop-block"
	export LDFLAGS="-Wl,-rpath,/usr/lib/bluegriffon-0.9.1 -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
	export PYTHON=python2

	autoconf-2.13

	make -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}
package() {
	cd "$srcdir"/mozilla-$_mozver

# No Make install for now :(
#	make DESTDIR="$pkgdir/" -C bg-trunk install

	install -d -m 755 ${pkgdir}/usr/bin

	install -d -m 755 ${pkgdir}/usr/lib/$_bgname-$_bgver

#	VOIR : https://github.com/remicollet/remirepo/blob/master/bluegriffon/bluegriffon.spec
	cd bg-trunk
	tar --create --file - --dereference --directory=dist/bin --exclude xulrunner . \
	| tar --extract --file - --directory ${pkgdir}/usr/lib/$_bgname-$_bgver

	cd ${pkgdir}/usr/bin && \
	ln -sf ../lib/$_bgname-$_bgver/$_bgname $_bgname


	cd "$srcdir"/mozilla-$_mozver

# 	Icons
	install -D -m 644 $_bgname/app/icons/default16.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/$_bgname.png"
	install -D -m 644 $_bgname/app/icons/default32.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/$_bgname.png"
	install -D -m 644 $_bgname/app/icons/default48.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/$_bgname.png"
	install -D -m 644 $_bgname/app/icons/default50.png "$pkgdir/usr/share/icons/hicolor/64x64/apps/$_bgname.png"
	install -D -m 644 $_bgname/app/icons/bluegriffon128.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/$_bgname.png"

#	Desktop
	install -D -m 644 "$srcdir/$_bgname.desktop" "$pkgdir/usr/share/applications/$_bgname.desktop"

# 	Use the system hunspell dictionaries
	rm -rf "${pkgdir}/usr/lib/$_bgname-$_bgver/dictionaries"
	ln -sf /usr/share/myspell/dicts "${pkgdir}/usr/lib/$_bgname-$_bgver/dictionaries"


}
