# Maintainer: Name <name@fai.com>

pkgname=blender-git
pkgver=2.80.57.20190418
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64')
license=('GPL')
url="http://blender.org/"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'
          'libjpeg' 'libpng' 'openjpeg' 'libtiff' 'openexr' 
          'python'
          'libsamplerate' 'ffmpeg' 'fftw'
          'openal' 'freetype2' 'libsndfile'
          'sdl2'
          'libgl' 'glfw-x11'
          'libxi' 'libxmu' 'libxxf86vm'
          'boost-libs'
          'jemalloc'
          'opencolorio'
          'python-numpy'
          'opencollada-git' 'openshadinglanguage-git' 'openimageio-git'
          'opensubdiv-git' 'openvdb-git' 'alembic-git'
          'llvm-libs'
)
makedepends=('cmake' 'boost' 'mesa' 'llvm'
             'cuda'
             'git'
)
optdepends=('cuda: cycles renderer cuda support')
conflicts=('blender')
provides=('blender')
options=('!strip')
#install="blender.install"
source=("$pkgname-$pkgver.tar.gz"
        "fix-TypeError-node_wrangler.patch"
        "fix-TypeError-mesh_discombobulator.patch"
        "fix-SyntaxError-io_convert_image_to_mesh_img.patch"
        "fix-SyntaxError-curve_tools.patch"
        "fix-SyntaxError-io_scene_cod.patch"
)
sha256sums=('251f7f73f9de120a1d5aa7dfee4d21de6b22164531c02999f4b50693359179c5'
            '31d3da481394b1a5af5cbcefc07fadca87dda6d91b9468eea4fbcb99dd72c9ab'
            '8b389543a34a933f53264e4cc33870eaa9adb684bfb6f8b146f5a7731109c1a0'
            '0b62f60ef3afefff26c997bcefd343f5d8e026ea4e22ca0244474a862fa1fe84'
            '5b4ec5ac68e444a4d0872af2aa68ddcc632dad981ef9bbad4a1fb84bf045ccfe'
            'e2ce934c3f965e492e6508536fa79264f4e4e998f23188e509d19c9047e69794'
)

_gitname="blender"
_slot="2.80"

prepare() {
  cd "${_gitname}"
  # TypeError: UILayout.label(): required parameter "text" to be a keyword argument!
  patch -Np1 -i "${srcdir}/fix-TypeError-node_wrangler.patch"
  patch -Np1 -i "${srcdir}/fix-TypeError-mesh_discombobulator.patch"

  # fix erreur : SyntaxError: positional argument follows keyword argument
  patch -Np1 -i "${srcdir}/fix-SyntaxError-io_convert_image_to_mesh_img.patch"
  patch -Np1 -i "${srcdir}/fix-SyntaxError-curve_tools.patch"
  patch -Np1 -i "${srcdir}/fix-SyntaxError-io_scene_cod.patch"

  # fix erreur : Referenced library 'python3.5' is an uninstalled dependency
  sed -i -e "s/python3.5/python/" release/scripts/addons/io_blend_utils/install_whl.py

  # Cycles: Workaround for performance loss with the CUDA 9.0 SDK.
  # Commit 1febc858559c054603073301a6c6dae44c737830 du 21/11/2017. Stefan Werner
  sed -i -e "s|define CUDA_KERNEL_MAX_REGISTERS 48|define CUDA_KERNEL_MAX_REGISTERS 64|" intern/cycles/kernel/kernels/cuda/kernel_config.h
  
  # Buildinfo : Workaround for HASH, BRANCH et COMMIT_TIMESTAMP
  sed -i -e 's|MY_WC_HASH "unknown"|MY_WC_HASH "63bae864f403"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_BRANCH "unknown"|MY_WC_BRANCH "master"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_COMMIT_TIMESTAMP 0|MY_WC_COMMIT_TIMESTAMP 1555579328|' build_files/cmake/buildinfo.cmake
}

build() {
  cd "${_gitname}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_BUILDINFO=ON \
        -DWITH_FFTW3=ON \
        -DWITH_OPENCOLORIO=ON \
        -DWITH_OPENSUBDIV=ON \
        -DWITH_OPENVDB=ON \
        -DWITH_OPENVDB_BLOSC=ON \
        -DWITH_MOD_OCEANSIM=ON \
        -DWITH_OPENIMAGEIO=ON \
        -DWITH_IMAGE_OPENEXR=ON \
        -DWITH_CODEC_FFMPEG=ON \
        -DWITH_CODEC_SNDFILE=ON \
        -DWITH_ALEMBIC=ON \
        -DWITH_OPENCOLLADA=ON \
        -DWITH_SDL=ON \
        -DWITH_INPUT_NDOF=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_PYTHON_INSTALL=OFF \
        -DWITH_PYTHON_INSTALL_NUMPY=OFF \
        -DWITH_PYTHON_INSTALL_REQUESTS=OFF \
        -DWITH_CYCLES_OSL=ON \
        -DWITH_CYCLES_CUDA_BINARIES=ON \
        -DCYCLES_CUDA_BINARIES_ARCH=sm_52 \
        -DWITH_LLVM=ON \
        -DPYTHON_VERSION=3.7 \
        -DPYTHON_LIBPATH=/usr/lib \
        -DPYTHON_LIBRARY=python3.7m \
        -DPYTHON_INCLUDE_DIRS=/usr/include/python3.7m \
        ..
  make ${MAKEFLAGS}
}

package() {
  # Blender
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install

  # voir Community
  python -m compileall -d /usr/share/blender "${pkgdir}/usr/share/blender"
  python -O -m compileall -d /usr/share/blender "${pkgdir}/usr/share/blender"
}
