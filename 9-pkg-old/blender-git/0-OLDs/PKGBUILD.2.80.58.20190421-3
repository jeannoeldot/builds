# Maintainer: Name <name@fai.com>

pkgname=blender-git
pkgver=2.80.58.20190421
pkgrel=3
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64')
license=('GPL')
url="http://blender.org/"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'
          'libjpeg' 'libpng' 'openjpeg' 'libtiff' 'openexr' 
          'python'
          'libsamplerate' 'ffmpeg' 'fftw'
          'openal' 'freetype2' 'libsndfile'
          'sdl2'
          'libgl' 'glfw-x11'
          'libxi' 'libxmu' 'libxxf86vm'
          'boost-libs'
          'jemalloc'
          'opencolorio'
          'python-numpy'
          'opencollada-git' 'openshadinglanguage-git' 'openimageio-git'
          'opensubdiv-git' 'openvdb-git' 'alembic-git' 'embree-git'
          'llvm-libs'
)
makedepends=('cmake' 'boost' 'mesa' 'llvm'
             'cuda'
             'git'
)
optdepends=('cuda: cycles renderer cuda support')
conflicts=('blender')
provides=('blender')
options=('!strip')
#install="blender.install"
source=("$pkgname-$pkgver.tar.gz"
        "fix-TypeError-node_wrangler.patch"
        "fix-TypeError-mesh_discombobulator.patch"
        "fix-SyntaxError-curve_tools.patch"
        "fix-SyntaxError-io_scene_cod.patch"
)
sha256sums=('f8c1cb591a26e0b9c94f101270508e28619a32de825289808a74e5079d3d6468'
            '31d3da481394b1a5af5cbcefc07fadca87dda6d91b9468eea4fbcb99dd72c9ab'
            '74ef1b43d91311b618ec9208ca8e17b61d303fce7399f36e7e212d5199725f32'
            '67d163d3f05a472577a5ac86a609aea0529a24d22d989cd48d6e547e2c7706dc'
            '604b0cc1f72bc7ca68f30f2ab53759b45f36b920d57167953fa97308f9394cd7'
)

_gitname="blender"
_slot="2.80"

prepare() {
  cd "${_gitname}"
  # TypeError: UILayout.label(): required parameter "text" to be a keyword argument!
  patch -Np1 -i "${srcdir}/fix-TypeError-node_wrangler.patch"
  patch -Np1 -i "${srcdir}/fix-TypeError-mesh_discombobulator.patch"

  # fix erreur : SyntaxError: positional argument follows keyword argument
  patch -Np1 -i "${srcdir}/fix-SyntaxError-curve_tools.patch"
  patch -Np1 -i "${srcdir}/fix-SyntaxError-io_scene_cod.patch"

  # fix erreur : Referenced library 'python3.5' is an uninstalled dependency
  sed -i -e "s/python3.5/python/" release/scripts/addons/io_blend_utils/install_whl.py

  # Cycles: Workaround for performance loss with the CUDA 9.0 SDK.
  # Commit 1febc858559c054603073301a6c6dae44c737830 du 21/11/2017. Stefan Werner
  sed -i -e "s|define CUDA_KERNEL_MAX_REGISTERS 48|define CUDA_KERNEL_MAX_REGISTERS 64|" intern/cycles/kernel/kernels/cuda/kernel_config.h
  
  # Buildinfo : Workaround for HASH, BRANCH et COMMIT_TIMESTAMP
  sed -i -e 's|MY_WC_HASH "unknown"|MY_WC_HASH "797539e76ccf"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_BRANCH "unknown"|MY_WC_BRANCH "master"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_COMMIT_TIMESTAMP 0|MY_WC_COMMIT_TIMESTAMP 1555878608|' build_files/cmake/buildinfo.cmake
}

build() {
  cd "${_gitname}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DWITH_BUILDINFO=ON \
        -DWITH_FFTW3=ON \
        -DWITH_OPENCOLORIO=ON \
        -DWITH_OPENSUBDIV=ON \
        -DWITH_OPENVDB=ON \
        -DWITH_OPENVDB_BLOSC=ON \
        -DWITH_MOD_OCEANSIM=ON \
        -DWITH_OPENIMAGEIO=ON \
        -DWITH_IMAGE_OPENEXR=ON \
        -DWITH_CODEC_FFMPEG=ON \
        -DWITH_CODEC_SNDFILE=ON \
        -DWITH_ALEMBIC=ON \
        -DWITH_OPENCOLLADA=ON \
        -DWITH_SDL=ON \
        -DWITH_INPUT_NDOF=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_PYTHON_INSTALL=OFF \
        -DWITH_PYTHON_INSTALL_NUMPY=OFF \
        -DWITH_PYTHON_INSTALL_REQUESTS=OFF \
        -DWITH_CYCLES_OSL=ON \
        -DWITH_CYCLES_EMBREE=ON \
        -DWITH_CYCLES_CUDA_BINARIES=ON \
        -DCYCLES_CUDA_BINARIES_ARCH=sm_52 \
        -DWITH_LLVM=ON \
        -DPYTHON_VERSION=3.7 \
        -DPYTHON_LIBPATH=/usr/lib \
        -DPYTHON_LIBRARY=python3.7m \
        -DPYTHON_INCLUDE_DIRS=/usr/include/python3.7m \
        ..
  make ${MAKEFLAGS}
}

package() {
  # Blender
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install

  # voir Community
  python -m compileall -d /usr/share/blender "${pkgdir}/usr/share/blender"
  python -O -m compileall -d /usr/share/blender "${pkgdir}/usr/share/blender"
}
