# Maintainer: Name <name@fai.com>

pkgname=blender-git
pkgver=2.91.0.20200823
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('x86_64')
license=('GPL')
url="http://blender.org/"
depends=('desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'
          'libjpeg' 'libpng' 'openjpeg2' 'libtiff' 'openexr' 
          'python'
          'libsamplerate' 'ffmpeg' 'fftw'
          'openal' 'freetype2' 'libsndfile'
          'sdl2'
          'libgl' 'glfw-x11'
          'libxi' 'libxmu' 'libxxf86vm' 'libxfixes' 'libxrender'
          'boost-libs' 'tbb'
          'jemalloc'
          'opencolorio'
          'python-numpy' 'python-requests'
          'llvm-libs'
          'opencollada-git' 'openshadinglanguage-git' 'openimageio-git'
          'opensubdiv-git' 'openvdb-git' 'alembic-git'
          'embree-git' 'openimagedenoise-git' 'usd-git'
)
makedepends=('cmake' 'boost' 'mesa' 'llvm'
             'cuda'
             'git'
)
optdepends=('cuda: cycles renderer cuda support')
conflicts=('blender')
provides=('blender')
#options=('!strip')
source=("$pkgname-$pkgver.tar.gz"
        "Fix-Embree-staticlibs-dynamiclibs.patch"
        "D8063-cuda11.patch"
        "OptiX-7.0.0-include.zip"
)
sha256sums=('f27acb4600dcd038b38f4d6fa51561e2c69a559982ad7e009bfd652dae3bcc37'
            'd46a0e6fe40cc61848287d52f6491cfa0b53ee195cd09c8654793d8243f366cd'
            '387229fa4954d583f75bf564fd067758971f2ccaf7000b2683011d733f545129'
            '2a26613f227453b133a0be70f10658562ed8e373e78536f15afc1dc8ef357a74'
)

_gitname="blender"
_slot="2.91"

prepare() {
  cd "${_gitname}"
  # Embree : static libs --> dynamic libs
  # https://github.com/bartoszek/AUR-blender-2.8-git
  patch -Np1 -i "${srcdir}/Fix-Embree-staticlibs-dynamiclibs.patch"

  # Cycles: Preliminary Cuda 11 build support
  # https://developer.blender.org/D8063
  patch -Np1 -i "${srcdir}/D8063-cuda11.patch"

  # Cycles: Workaround for performance loss with the CUDA 9.0 SDK.
  # Commit 1febc858559c054603073301a6c6dae44c737830 du 21/11/2017. Stefan Werner
  sed -i -e "s|define CUDA_KERNEL_MAX_REGISTERS 48|define CUDA_KERNEL_MAX_REGISTERS 64|" intern/cycles/kernel/kernels/cuda/kernel_config.h
  
  # Buildinfo : Workaround for HASH, BRANCH et COMMIT_TIMESTAMP
  sed -i -e 's|MY_WC_HASH "unknown"|MY_WC_HASH "e4932d1167f4"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_BRANCH "unknown"|MY_WC_BRANCH "master"|' build_files/cmake/buildinfo.cmake
  sed -i -e 's|MY_WC_COMMIT_TIMESTAMP 0|MY_WC_COMMIT_TIMESTAMP 1598186924|' build_files/cmake/buildinfo.cmake
}

build() {
  cd "${_gitname}"
  
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_C_FLAGS_RELEASE:STRING=-DNDEBUG \
        -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
        -DCMAKE_SKIP_RPATH=ON \
        -DOPTIX_ROOT_DIR="${srcdir}"/include \
        -DWITH_XR_OPENXR=OFF \
        -DWITH_JACK=OFF \
        -DWITH_INPUT_NDOF=OFF \
        -DWITH_INSTALL_PORTABLE=OFF \
        -DWITH_PYTHON_INSTALL=OFF \
        -DWITH_PYTHON_INSTALL_NUMPY=OFF \
        -DWITH_PYTHON_INSTALL_REQUESTS=OFF \
        -DWITH_CYCLES_CUDA_BINARIES=ON \
        -DCYCLES_CUDA_BINARIES_ARCH="sm_52;compute_80" \
        -DWITH_CYCLES_NATIVE_ONLY=ON \
        -DWITH_CYCLES_DEVICE_OPTIX=ON \
        -DWITH_LLVM=ON \
        -DPYTHON_VERSION=3.8 \
        -DPYTHON_LIBPATH=/usr/lib \
        -DPYTHON_LIBRARY=python3.8 \
        -DPYTHON_INCLUDE_DIRS=/usr/include/python3.8 \
        ..
  make
}

package() {
  # Blender
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install

  # voir Community
  python -m compileall -d /usr/share/blender "${pkgdir}/usr/share/blender"
  python -O -m compileall -d /usr/share/blender "${pkgdir}/usr/share/blender"
  
  install -Dm644 ../release/freedesktop/org.blender.Blender.appdata.xml "${pkgdir}/usr/share/metainfo/org.blender.Blender.appdata.xml"
}
