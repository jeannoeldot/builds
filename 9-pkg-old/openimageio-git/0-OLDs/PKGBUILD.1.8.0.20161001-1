# $Id$
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=openimageio-git
pkgver=1.8.0.20161001
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('i686' 'x86_64')
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio' 'intel-tbb'
         'libjpeg-turbo' 'openjpeg' 'libraw'
         'ffmpeg'
         'opencv' 'openssl'
)
makedepends=('cmake' 'qt4' 'python2' 'boost' 'mesa')
optdepends=('qt4: iv image viewer'
            'python2: bindings support')
conflicts=('openimageio')
provides=('openimageio')
source=("$pkgname-$pkgver.tar.gz"
        "fix-gcc-error-about-casting-for-AVX2.patch"
)
sha256sums=('2aa4c466bd056ec7fd8e7e2231cd9e973a9cb895a2cc3a0a80d533fad1298884'
            '2b459a9e2b2c1d32e1d04ff0c526e36de385ae444d6592c8c6d0cae0b3c4b59c'
)

_gitname="oiio"

prepare() {
  cd "${srcdir}/${_gitname}"
   
  # Fix gcc warning about casting for AVX2
  patch -Np1 -i "${srcdir}/fix-gcc-error-about-casting-for-AVX2.patch"
}
  
build() {
  cd "${_gitname}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_SKIP_RPATH=ON \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DSTOP_ON_WARNING=OFF \
      -DOIIO_BUILD_TESTS=OFF \
      -DPYTHON_VERSION=2.7 \
      -DPYTHON3_VERSION=3.5 \
      -DUSE_OPENSSL=ON \
      -DUSE_PTEX=OFF \
      -DUSE_NUKE=OFF \
      -DUSE_SIMD=avx2 \
      -DUSE_CCACHE=OFF \
      ..
  make
}

package() {
  cd "${_gitname}/build"

  make DESTDIR="${pkgdir}" install

  # license
  cd ..
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
