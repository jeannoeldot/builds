# Maintainer: Name <name@fai.com>

pkgname=openimageio-git
pkgver=2.4.4.2.20221003
pkgrel=1
_name_release="oiio-2.4.4.2"
_ver_release="v2.4.4.2"
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('x86_64')
url="http://www.openimageio.org/"
license=('custom')
# TODO: Consider adding these deps: 'openvdb' 'tbb' 'ptex' 'opencv'
depends=('openexr' 'boost-libs' 'libtiff'
         'libjpeg-turbo' 'openjpeg2' 'libraw' 'ffmpeg'
         'qt5-base'
         'python'
         'libwebp' 'libheif'
         'freetype2'
         'pugixml' 'pybind11' 'python-numpy'
         'opencolorio'
)
makedepends=('cmake' 'boost' 'mesa'
             'robin-map'
             'git'
)
conflicts=('openimageio')
provides=('openimageio')
source=("oiio-${_ver_release}.tar.gz"
)
sha256sums=('1ae437e1178f53a972d8b42147d7571c5463652a638b36526f25d5719becbb55'
)

build() {
  cd "${_name_release}"

# FIXME: BUILD_FMT_FORCE=ON is used below to bundle fmt 8 so OSL builds
# TODO: test without this and remove it if OSL (1.12?) works with fmt 9

  cmake \
      -Bbuild \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DVERBOSE=ON \
      -DOIIO_BUILD_TESTS=OFF \
      -DINSTALL_FONTS=OFF \
      -DUSE_CCACHE=OFF \
      -DUSE_SIMD=avx2 \
      -DPYTHON_VERSION=3.10 \
      -DBUILD_FMT_FORCE=ON \
      -DOIIO_DOWNLOAD_MISSING_TESTDATA=OFF \
      -DUSE_EXTERNAL_PUGIXML=ON
  make -C build
}

package() {
  cd "${_name_release}"

  DESTDIR="${pkgdir}" make -C build install

  # license
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 THIRD-PARTY.md "${pkgdir}"/usr/share/licenses/${pkgname}/THIRD-PARTY
}
