# Maintainer: Moi <name AT fai DOT fr>

pkgname=usd-git
pkgver=21.02.20210120
pkgrel=4
_ver_release="21.02"
_name_release="USD-${_ver_release}"
pkgdesc="Universal Scene Description"
url="http://www.openusd.org/"
arch=('x86_64')
license=('custom')
depends=('boost-libs' 'tbb2020'
)
makedepends=('boost' 'cmake' 'python'
)
conflicts=('usd')
provides=('usd')
source=("USD-v${_ver_release}.tar.gz"
        "blender-usd-diff.patch"
        "Fix-build-with-GCC11.patch"
)
sha256sums=('97bbae5d86d6e4db357645db183be03afe28fe024b242f27bd4f62b4e2eade53'
            '42f5aa80b5055a3ce0c139059a0e260fed709d82af736dec2e8e22ee4fffeb9c'
            '46e35b82e04c1b589256b29cceacd2e6fdf3cbee0d12ff64379e8344bf763e47'
)

prepare() {
  cd "${_name_release}"
  # blender/build_files/build_environment/patches/usd.diff
  patch -Np1 -i "${srcdir}/blender-usd-diff.patch"
  # Fix build with GCC 11
  patch -Np1 -i "${srcdir}/Fix-build-with-GCC11.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DTBB_INCLUDE_DIR=/usr/include/tbb2020 \
    -DTBB_LIBRARY=/usr/lib/tbb2020 \
    -DPXR_SET_INTERNAL_NAMESPACE=usdBlender \
    -DPXR_ENABLE_PYTHON_SUPPORT=OFF \
    -DPXR_BUILD_IMAGING=OFF \
    -DPXR_BUILD_TESTS=OFF \
    -DPXR_BUILD_EXAMPLES=OFF \
    -DPXR_BUILD_TUTORIALS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DPXR_BUILD_MONOLITHIC=ON \
    -DPXR_BUILD_USD_TOOLS=OFF \
    -DCMAKE_DEBUG_POSTFIX=_d \
    ..
  find . -type f -exec sed -i -e 's/\-O3//g' {} \;
  make VERBOSE=1
}

package() {
  cd "${_name_release}/build"
  make DESTDIR="${pkgdir}" install
  
  # pxrConfig.cmake
  mkdir -p "${pkgdir}"/usr/lib/cmake/pxr
  mv "${pkgdir}"/usr/pxrConfig.cmake "${pkgdir}"/usr/lib/cmake/pxr
  # plugin
  mkdir -p "${pkgdir}"/usr/share/usd
  mv "${pkgdir}"/usr/plugin "${pkgdir}"/usr/share/usd/plugin
  
  #license
  cd ..
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
