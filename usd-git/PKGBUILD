# Maintainer: Moi <name AT fai DOT fr>

pkgname=usd-git
pkgver=20.05.20200414
pkgrel=2
_ver_release="20.05"
_name_release="USD-${_ver_release}"
pkgdesc="Universal Scene Description"
url="http://www.openusd.org/"
arch=('x86_64')
license=('custom')
depends=('boost-libs' 'tbb'
)
makedepends=('boost' 'cmake' 'python'
)
conflicts=('usd')
provides=('usd')
source=("USD-v${_ver_release}.tar.gz"
        "blender-usd-diff.patch"
)
sha256sums=('622403872f530fe7d9d291dc8a98132c5bc979f64b82b834718d4e26aacfb9fd'
            'db24d6aa9362222a6959bd9e0c05284ffd05a42e38fb7d53ecec7a11ad5dfe67'
)

prepare() {
  cd "${_name_release}"
  # blender/build_files/build_environment/patches/usd.diff
  patch -Np1 -i "${srcdir}/blender-usd-diff.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DPXR_SET_INTERNAL_NAMESPACE=usdBlender \
    -DPXR_ENABLE_PYTHON_SUPPORT=OFF \
    -DPXR_BUILD_IMAGING=OFF \
    -DPXR_BUILD_TESTS=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DPXR_BUILD_MONOLITHIC=ON \
    -DPXR_BUILD_USD_TOOLS=OFF \
    -DCMAKE_DEBUG_POSTFIX=_d \
    ..
  find . -type f -exec sed -i -e 's/\-O3//g' {} \;
  make VERBOSE=1
}

package() {
  cd "${_name_release}/build"
  make DESTDIR="${pkgdir}" install
  
  # pxrConfig.cmake
  mkdir -p "${pkgdir}"/usr/lib/cmake/pxr
  mv "${pkgdir}"/usr/pxrConfig.cmake "${pkgdir}"/usr/lib/cmake/pxr
  # plugin
  mkdir -p "${pkgdir}"/usr/share/usd
  mv "${pkgdir}"/usr/plugin "${pkgdir}"/usr/share/usd/plugin
  
  #license
  cd ..
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
