# Maintainer: Moi <name AT fai DOT fr>

pkgname=lm_sensors-git
pkgver=3.5.0.20190123
pkgrel=1

_name_release="lm-sensors"

pkgdesc="Collection of user space tools for general SMBus access and hardware monitoring"
url="https://github.com/lm-sensors/lm_sensors"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('perl'
)
makedepends=('git' 'rrdtool'
)
optdepends=('rrdtool: for logging with sensord')
conflicts=('lm_sensors')
provides=('lm_sensors')
backup=('etc/sensors3.conf' 'etc/healthd.conf' 'etc/conf.d/sensord')
source=("${_name_release}-git-${pkgver}.tar.gz"
        "healthd"
        "healthd.conf"
        "healthd.service"
        "sensord.conf"
)
sha256sums=('114c8891e546854807384c7eeeda4f9cd7426b06c490d031a089f4b6a073bbe5'
            '0ac9afb2a9155dd74ab393756ed552cd542dde1081149beb2ab4ec7ff55b8f4a'
            '5d17a366b175cf9cb4bb0115c030d4b8d91231546f713784a74935b6e533da9f'
            '2638cd363e60f8d36bcac468f414a6ba29a1b5599f40fc651ca953858c8429d7'
            '23bebef4c250f8c0aaba2c75fd3d2c8ee9473cc91a342161a9f5b3a34ddfa9e5'
)

prepare() {
  cd "${srcdir}/lm-sensors"
  sed -i 's|/etc/sysconfig|/etc/conf.d|' prog/{detect/sensors-detect,init/{sensord,lm_sensors}.service}
  sed -i 's/EnvironmentFile=/EnvironmentFile=-/' prog/init/lm_sensors.service
}

build() {
  cd "${srcdir}/lm-sensors"
  make PREFIX=/usr
}

package() {
  cd "${srcdir}/lm-sensors"
  make PROG_EXTRA=sensord BUILD_STATIC_LIB=0 \
    PREFIX=/usr SBINDIR=/usr/bin MANDIR=/usr/share/man DESTDIR="${pkgdir}" install

  install -D -m755 "${srcdir}/healthd" "${pkgdir}/usr/bin/healthd"

  install -D -m644 "${srcdir}/healthd.conf" "${pkgdir}/etc/healthd.conf"
  install -D -m644 "${srcdir}/sensord.conf" "${pkgdir}/etc/conf.d/sensord"
 
  install -D -m644 "${srcdir}/healthd.service" "${pkgdir}/usr/lib/systemd/system/healthd.service"
  install -D -m644 prog/init/*.service "${pkgdir}/usr/lib/systemd/system/"
}
