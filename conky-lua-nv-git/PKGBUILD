# Maintainer: Name <name@fai.com>

pkgname=conky-lua-nv-git
pkgver=1.11.4.20190416
pkgrel=3
pkgdesc="An advanced system monitor for X based on torsmo with lua and nvidia enabled"
arch=('i686' 'x86_64')
url="https://github.com/brndnmtthws/conky"
license=('GPL3')
conflicts=('conky')
provides=('conky')
depends=('alsa-lib' 'libxml2' 'curl' 'cairo' 'wireless_tools'
          'libxft' 'glib2' 'libxdamage' 'imlib2' 'lua' 'librsvg'
          'libxinerama' 'libxnvctrl'
)
makedepends=('docbook2x' 'perl-xml-libxml'
             'docbook-xml' 'docbook-xsl' 'cmake'  'git'
)
optdepends=('nvidia: for GTxxx and newer GPUs',
)
source=("$pkgname-$pkgver.tar.gz"
)
sha256sums=('9b08157ace3d0d827b0f874fd1c8439c09e097792f052e53c0c03903701101d8'
)
#options=('!strip' 'debug')

_gitname="conky"

build() {
  cd "${_gitname}"

  cmake \
    -D CMAKE_SKIP_RPATH=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D BUILD_DOCS=ON \
    -D BUILD_NVIDIA=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_LUA_CAIRO=ON \
    -D BUILD_LUA_IMLIB2=ON \
    -D BUILD_LUA_RSVG=ON \
    -D BUILD_MPD=OFF \
    -D BUILD_MOC=OFF \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D BUILD_WEATHER_METAR=OFF \
    -D BUILD_CMUS=OFF \
    -D CMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
#  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
#  install -D -m644 LICENSE.BSD "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.BSD
  install -D -m644 extras/gedit/conky.lang "${pkgdir}"/usr/share/gtksourceview-3.0/language-specs/conky.lang
  install -D -m644 extras/nano/conky.nanorc "${pkgdir}"/usr/share/nano/conky.nanorc
  install -D -m644 extras/vim/syntax/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax/conkyrc.vim
  install -D -m644 extras/vim/ftdetect/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}
