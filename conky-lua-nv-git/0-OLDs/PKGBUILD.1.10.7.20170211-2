# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=conky-lua-nv-git
pkgver=1.10.7.20170211
pkgrel=2
pkgdesc="An advanced system monitor for X based on torsmo with lua and nvidia enabled"
arch=('i686' 'x86_64')
url="https://github.com/brndnmtthws/conky"
license=('GPL3' 'BSD')
replaces=('torsmo' 'conky')
conflicts=('conky' 'conky-lua')
provides=('conky' 'conky-lua')
depends=('alsa-lib' 'libxml2' 'curl' 'cairo' 'wireless_tools'
          'libxft' 'glib2' 'libxdamage' 'imlib2' 'lua51' 'librsvg' 'tolua++'
          'libxinerama' 'libxnvctrl'
)
makedepends=('docbook2x' 'perl-xml-libxml'
             'docbook-xml' 'docbook-xsl' 'cmake' 'git'
)
optdepends=('nvidia: for GT4xx and newer GPUs',
            'nvidia-340xx: for G8x, G9x, GT2xx GPUS',
            'nvidia-304xx: for GeForce 6/7 GPUs'
)
source=("$pkgname-$pkgver.tar.gz"
        'Fix-error-when-generating-man-pages-from-XML.patch'
        'Fix-build-with-gcc7-missing-include.patch'
)
sha256sums=('5c44264c2bf2e37dd4bba33fb6bad1c39cffab8911a03b18e2fa70c4c0467cb9'
            'ee270f55921d42205741b8ae7efbc7d1801bc57ebcd721cb41f4987cf22c0d7f'
            'dbf683262810462189f5badce8bbd3d3ba4d312d86a14323447816d0b5f72591'
)
#options=('!strip' 'debug')
install="conky-lua-nv-git.install"

_gitname="conky"

prepare() {
  cd "${_gitname}"
  # https://github.com/brndnmtthws/conky/pull/370/commits/b9def6994c439188d75dba2bf78a1dcf8acceb4b
  patch -Np1 -i "${srcdir}/Fix-error-when-generating-man-pages-from-XML.patch"
  # https://github.com/brndnmtthws/conky/issues/396
  patch -Np1 -i "${srcdir}/Fix-build-with-gcc7-missing-include.patch"
}

build() {
  cd "${_gitname}"

  cmake \
    -D CMAKE_SKIP_RPATH=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D MAINTAINER_MODE=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_NVIDIA=ON \
    -D BUILD_XDBE=ON \
    -D BUILD_IMLIB2=ON \
    -D BUILD_LUA_CAIRO=ON \
    -D BUILD_LUA_IMLIB2=ON \
    -D BUILD_LUA_RSVG=ON \
    -D BUILD_MPD=OFF \
    -D BUILD_MOC=OFF \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D CMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 LICENSE.BSD "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.BSD
  install -D -m644 extras/gedit/conky.lang "${pkgdir}"/usr/share/gtksourceview-3.0/language-specs/conky.lang
  install -D -m644 extras/nano/conky.nanorc "${pkgdir}"/usr/share/nano/conky.nanorc
  install -D -m644 extras/vim/syntax/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax/conkyrc.vim
  install -D -m644 extras/vim/ftdetect/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}
