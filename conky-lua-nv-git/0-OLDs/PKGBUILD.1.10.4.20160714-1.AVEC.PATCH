# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=conky-lua-nv-git
pkgver=1.10.4.20160714
pkgrel=1
pkgdesc="An advanced system monitor for X based on torsmo with lua and nvidia enabled"
arch=('i686' 'x86_64')
url="https://github.com/brndnmtthws/conky"
license=('GPL3' 'BSD')
replaces=('torsmo' 'conky')
conflicts=('conky' 'conky-lua')
provides=('conky' 'conky-lua')
depends=('alsa-lib' 'libxml2' 'curl' 'cairo' 'wireless_tools'
          'libxft' 'glib2' 'libxdamage' 'imlib2' 'lua51' 'librsvg' 'tolua++'
          'libxinerama'
)
makedepends=('docbook2x' 'libxnvctrl' 'perl-xml-libxml'
             'docbook-xml' 'docbook-xsl' 'cmake' 'git'
)
optdepends=('nvidia: for GT4xx and newer GPUs',
            'nvidia-340xx: for G8x, G9x, GT2xx GPUS',
            'nvidia-304xx: for GeForce 6/7 GPUs'
)
source=("$pkgname-$pkgver.tar.gz"
        'ascii.patch'
        'fix-build.patch'
)
sha256sums=('eae95e755c8c72b1de7c20c1dcc240ff80cfbfd7ba4c718575db163214b32087'
            'dd1290aa18675967b7437ba46785e30b2ef14846620c9379c6dfe83ea9dd7895'
            '6a42ce02f936ebb08543b25c5d9111f5f3eaad65e85361616bd0cdcd525681b1'
)
#options=('!strip' 'debug')
install="conky-lua-nv-git.install"

_gitname="conky"

prepare() {
  cd "${_gitname}"
  patch -Np1 -i "${srcdir}/ascii.patch"
  patch -Np1 -i "${srcdir}/fix-build.patch"
}

build() {
  cd "${_gitname}"

  cmake \
    -D CMAKE_SKIP_RPATH=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D MAINTAINER_MODE=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_NVIDIA=ON \
    -D BUILD_XDBE=ON \
    -D BUILD_IMLIB2=ON \
    -D BUILD_LUA_CAIRO=ON \
    -D BUILD_LUA_IMLIB2=ON \
    -D BUILD_LUA_RSVG=ON \
    -D BUILD_MOC=OFF \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D CMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 LICENSE.BSD "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.BSD
  install -D -m644 extras/gedit/conky.lang "${pkgdir}"/usr/share/gtksourceview-3.0/language-specs/conky.lang
  install -D -m644 extras/nano/conky.nanorc "${pkgdir}"/usr/share/nano/conky.nanorc
  install -D -m644 extras/vim/syntax/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/syntax/conkyrc.vim
  install -D -m644 extras/vim/ftdetect/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}
