# Maintainer: Name <name@fai.com>

pkgname=conky-lua-nv-git
pkgver=1.14.1.20221015
pkgrel=1
pkgdesc="An advanced system monitor for X based on torsmo with lua and nvidia enabled"
arch=('i686' 'x86_64')
url="https://github.com/brndnmtthws/conky"
license=('GPL3')
conflicts=('conky')
provides=('conky')
depends=('libxml2' 'curl' 'cairo' 'wireless_tools'
          'libxft' 'glib2' 'libxdamage' 'imlib2' 'lua' 'librsvg'
          'libxinerama' 'libxnvctrl'
          'hicolor-icon-theme'
          'libpulse'
)
makedepends=('cmake'  'git' 'man-db'
)
optdepends=('nvidia: for GTxxx and newer GPUs',
)
source=("$pkgname-$pkgver.tar.gz"
)
sha256sums=('78a2b1aa46cd98d11f236c108eda9359882d330b0303cf814fa66911f82060f5'
)
#options=('!strip' 'debug')

_gitname="conky"

prepare() {
  cd "${_gitname}"
  # Use our CFLAGS / CXXFLAGS
  sed -i '/set(CMAKE_C_FLAGS/,+5d' cmake/ConkyBuildOptions.cmake
}

build() {
  cd "${_gitname}"

  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -B build \
    -D MAINTAINER_MODE=OFF \
    -D CMAKE_SKIP_RPATH=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS_RELEASE:STRING=-DNDEBUG \
    -D CMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
    -D BUILD_NVIDIA=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_LUA_CAIRO=ON \
    -D BUILD_LUA_IMLIB2=ON \
    -D BUILD_LUA_RSVG=ON \
    -D BUILD_MPD=OFF \
    -D BUILD_MOC=OFF \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D BUILD_CMUS=OFF \
    -D CMAKE_INSTALL_PREFIX=/usr
  make -C build VERBOSE=1
}

package() {
  cd "${_gitname}"
  DESTDIR="${pkgdir}" make -C build install
#  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
#  install -D -m644 LICENSE.BSD "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.BSD
  install -D -m644 extras/gedit/conky.lang "${pkgdir}"/usr/share/gtksourceview-3.0/language-specs/conky.lang
  install -D -m644 extras/nano/conky.nanorc.j2 "${pkgdir}"/usr/share/nano/conky.nanorc
  install -D -m644 extras/vim/syntax/conkyrc.vim.j2 "${pkgdir}"/usr/share/vim/vimfiles/syntax/conkyrc.vim
  install -D -m644 extras/vim/ftdetect/conkyrc.vim "${pkgdir}"/usr/share/vim/vimfiles/ftdetect/conkyrc.vim
}
