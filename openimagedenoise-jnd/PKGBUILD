# Maintainer: Moi <name AT fai DOT fr>

pkgname=openimagedenoise-jnd
pkgver=2.3.3.20250404
pkgrel=3
_ver_release="2.3.3"
_name_release="oidn-${_ver_release}"
pkgdesc="Intel(R) Open Image Denoise library"
url="http://www.openimagedenoise.org/"
arch=('x86_64')
license=('Apache-2.0')
depends=('onetbb' 'nvidia-utils'
)
makedepends=('cmake' 'python' 'ispc'
              'cuda'
)
conflicts=('openimagedenoise' 'openimagedenoise-git' 'oidn')
provides=('openimagedenoise' 'oidn')
#options=(staticlibs)
options=('!debug')
source=("oidn-${_ver_release}.src.tar.gz"
        "0001-Fix-build-with-CUDA-13.patch"
)
sha256sums=('ccf221535b4007607fb53d3ff5afa74de25413bb8ef5d03d215f46c7cc2f96cf'
            'a7c3c933675c837af8284d357c0abbb6d6c6b21e6738a06ce2db8ce4cc6f449a'
)

prepare() {
  cd "${_name_release}"
  # # Fix build with CUDA 13 https://github.com/RenderKit/oidn/issues/274
  patch -Np1 -i "${srcdir}/0001-Fix-build-with-CUDA-13.patch"

}

build() {
  cd "${_name_release}"

  cmake \
    -Bbuild \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=ON \
    -DOIDN_DEVICE_CUDA=ON
  make -C build
}

package() {
  cd "${_name_release}"
  
  DESTDIR="${pkgdir}" make -C build install

  rm -rf "${pkgdir}"/build
}
