# Maintainer: Name <name@fai.com>

pkgname=openshadinglanguage-git
pkgver=1.10.6.20190708
pkgrel=5
_name_release="OpenShadingLanguage-Release-1.10.6"
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'openimageio-git'
         'llvm-libs'
         'qt5-base'
)
makedepends=('boost' 'cmake' 'llvm' 'clang' 'python')
conflicts=('openshadinglanguage')
provides=('openshadinglanguage')
source=("$_name_release.tar.gz"
)
sha256sums=('6ce825e9f224e7cae460a19d2b926700c9db1789e3c765933b426dd0f9562160'
)

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_C_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DLLVM_STATIC=OFF \
      -DVERBOSE=ON \
      -DOSL_BUILD_TESTS=OFF \
      -DUSE_LLVM_BITCODE=OFF \
      -DUSE_PARTIO=OFF \
      -DUSE_SIMD=avx2 \
      -DSTOP_ON_WARNING=OFF \
      -DUSE_CCACHE=OFF \
    ..
  make VERBOSE=1
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install

  # license
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  mv -v "${pkgdir}"/usr/share/doc/OSL/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}

  # Ugly hacks /usr/shaders/*.oso and /usr/shaders/MaterialX/*.oso
  for _oso in $(find "${pkgdir}"/usr/shaders/*.oso -type f)
  do
    sed -i "s|/build/${pkgname}/src/${_name_release}/src/|/usr/|g" ${_oso}
    sed -i "s|/build/${pkgname}/src/${_name_release}/build/src/|/usr/|g" ${_oso}
  done

  for _oso in $(find "${pkgdir}"/usr/shaders/MaterialX/*.oso -type f)
  do
    sed -i "s|/build/${pkgname}/src/${_name_release}/src/|/usr/|g" ${_oso}
    sed -i "s|/build/${pkgname}/src/${_name_release}/build/src/|/usr/|g" ${_oso}
  done
}
