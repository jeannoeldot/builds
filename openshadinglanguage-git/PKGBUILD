# Maintainer: Name <name@fai.com>

pkgname=openshadinglanguage-git
pkgver=1.11.17.0.20220110
pkgrel=10
_name_release="OpenShadingLanguage-1.11.17.0"
_ver_release="v1.11.17.0"
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/AcademySoftwareFoundation/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'imath' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'openimageio-git'
         'llvm-libs'
         'qt5-base'
         'clang'
         'python' 'pugixml' 'pybind11'
)
makedepends=('boost' 'cmake' 'llvm')
conflicts=('openshadinglanguage')
provides=('openshadinglanguage')
source=("OpenShadingLanguage-${_ver_release}.tar.gz"
        "llvm14.patch"
)
sha256sums=('ab7e17fde3f759b94efcc159d8f4ccc6cc92f40cba76e4f88a8133a00587738f'
            '0c47cc64bea838a7893441b1ab6ad760ad6b86f50ef9fbdf7c69eb16b27e415a'
)

prepare() {
  cd "${_name_release}"
  # https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/pull/1492
  # Support for LLVM 14 #1492
  patch -Np1 -i "${srcdir}/llvm14.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_CXX_STANDARD=14 \
      -DLLVM_STATIC=OFF \
      -DVERBOSE=ON \
      -DUSE_LLVM_BITCODE=OFF \
      -DUSE_SIMD=avx2 \
      -DUSE_CCACHE=OFF \
    ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install

  # license
  install -Dm644 ../LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # shaders
  # Ugly hacks /usr/share/OSL/shaders/*.oso
  for _oso in $(find "${pkgdir}"/usr/share/OSL/shaders/*.oso -type f)
  do
    sed -i "s|/build/${pkgname}/src/${_name_release}/src/|/usr/share/OSL/|g" ${_oso}
    sed -i "s|/build/${pkgname}/src/${_name_release}/build/src/|/usr/share/OSL/|g" ${_oso}
  done
}
