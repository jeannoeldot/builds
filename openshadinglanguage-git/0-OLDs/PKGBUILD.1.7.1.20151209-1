# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=openshadinglanguage-git
pkgver=1.7.1.20151209
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=(i686 x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'libwebp'
         'nvidia-libgl'
         'openimageio-git'
)
makedepends=('boost' 'cmake' 'clang35' 'llvm35')
provides=('openshadinglanguage')
conflicts=('openshadinglanguage')
source=($pkgname-$pkgver.tar.gz
        'llvm35-static-system-libs.patch'
)
sha256sums=('0ec431d201cafa94a64c4eaa85662613cd5c54eedaf52d0fdb1f48df5aa88d01'
            'e9546cec837ac55d0764389c5d4825ece09da1e97039fb8a51d46734dde5ae29'
)

_gitname="osl"

prepare() {
  cd "${srcdir}/${_gitname}"
   
  # Add system libraries needed to link against LLVM components (zlib, ncurses)
  # This is required in order to statically link against LLVM
  # VOIRÂ Community
  patch -Np1 -i "${srcdir}/llvm35-static-system-libs.patch"
}
  
build() {
  cd "${_gitname}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DSTOP_ON_WARNING=OFF \
      -DLLVM_STATIC=ON \
      -DOSL_BUILD_TESTS=OFF \
      -DOSL_BUILD_CPP11=ON \
      -DUSE_SIMD=sse4.2 \
      -DUSE_CCACHE=OFF \
    ..
  make
}

package() {
  cd "${_gitname}/build"

  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}"/usr/share/OSL/
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname} # TODO: Tell upstream about this shit
  mv "${pkgdir}"/usr/{CHANGES,README.md,INSTALL} "${pkgdir}"/usr/share/OSL/
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/OSL/doc
# !!! TESTS
#  mv "${pkgdir}"/usr/shaders "${pkgdir}"/usr/share/OSL/shaders
}
