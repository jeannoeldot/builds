# Maintainer: Name <name@fai.com>

pkgname=openshadinglanguage-git
pkgver=1.11.13.0.20210402
pkgrel=4
_name_release="OpenShadingLanguage-1.11.13.0"
_ver_release="v1.11.13.0"
pkgdesc="Advanced shading language for production GI renderers"
arch=('x86_64')
url="https://github.com/AcademySoftwareFoundation/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'imath' 'tbb' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'openimageio-git'
         'llvm-libs'
         'qt5-base'
         'clang'
         'python' 'pugixml' 'pybind11'
)
makedepends=('boost' 'cmake' 'llvm')
conflicts=('openshadinglanguage')
provides=('openshadinglanguage')
source=("OpenShadingLanguage-${_ver_release}.tar.gz"
        'Fix-crash-generating-closure-functions-calls-with-LLVM-11.patch'
)
sha256sums=('5fface4b487b9bf9448ac5f5a370dbe2075d2941dd11c396448a86ac11663002'
            'e7e4a832876874284c4be01490f47502f8befd7cd3bd74d005aebef2c0124ead'
)

prepare() {
  cd "${_name_release}"
  # https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/pull/1361/files
  # Fix crash generating closure functions calls with LLVM 11
  patch -Np1 -i "${srcdir}/Fix-crash-generating-closure-functions-calls-with-LLVM-11.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_CXX_STANDARD=14 \
      -DLLVM_STATIC=OFF \
      -DVERBOSE=ON \
      -DUSE_LLVM_BITCODE=OFF \
      -DUSE_SIMD=avx2 \
      -DUSE_CCACHE=OFF \
    ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install

  # license
  install -Dm644 ../LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # shaders
  # Ugly hacks /usr/share/OSL/shaders/*.oso
  for _oso in $(find "${pkgdir}"/usr/share/OSL/shaders/*.oso -type f)
  do
    sed -i "s|/build/${pkgname}/src/${_name_release}/src/|/usr/share/OSL/|g" ${_oso}
    sed -i "s|/build/${pkgname}/src/${_name_release}/build/src/|/usr/share/OSL/|g" ${_oso}
  done
}
