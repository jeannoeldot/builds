# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=openshadinglanguage-git
pkgver=1.8.7.20170401
pkgrel=2
_name_release="OpenShadingLanguage-Release-1.8.7"
pkgdesc="Advanced shading language for production GI renderers"
arch=('i686' 'x86_64')
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openexr' 'intel-tbb' 'freetype2' 'libpng' 'libtiff'
         'zlib' 'ncurses'
         'openimageio-git'
)
makedepends=('boost' 'cmake' 'llvm35' 'clang35')
conflicts=('openshadinglanguage')
provides=('openshadinglanguage')
source=("$_name_release.tar.gz"
        'llvm35-static-system-libs.patch'
)
sha256sums=('45ffae7bc6ccffa3bef483c1634d3b8fb4dd6a4939d5bbdd0bc133063c07eeff'
            '0e09aef60d64b958a5a36e9f31a0ab52eda698575f7ce752298acc906cede0b6'
)

prepare() {
  cd "${_name_release}"
   
  # Add system libraries needed to link against LLVM components (zlib, ncurses)
  # This is required in order to statically link against LLVM
  # VOIR Community
  patch -Np1 -i "${srcdir}/llvm35-static-system-libs.patch"
}

build() {
  cd "${_name_release}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DSTOP_ON_WARNING=OFF \
      -DLLVM_STATIC=ON \
      -DOSL_BUILD_TESTS=OFF \
      -DUSE_PARTIO=OFF \
      -DUSE_SIMD=avx2 \
      -DUSE_CCACHE=OFF \
    ..
  make
}

package() {
  cd "${_name_release}/build"

  make DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}"/usr/share/OSL/
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname} # TODO: Tell upstream about this shit
  mv "${pkgdir}"/usr/{INSTALL.md,CHANGES.md,README.md} "${pkgdir}"/usr/share/OSL/
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/OSL/doc
#  mv "${pkgdir}"/usr/shaders "${pkgdir}"/usr/share/OSL/shaders
}
