# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Vasco Costa <vasco.costa@meiodigital.com>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>
# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

pkgname=pokerth
pkgver=1.1.2
_pkgver=1.1.2-rc
pkgrel=2
pkgdesc="Open source Texas Hold'em Poker simulator"
arch=('i686' 'x86_64')
url="http://www.pokerth.net/"
license=('GPL' 'custom')
depends=('curl' 'boost-libs' 'gsasl' 'gnutls' 'protobuf' 'libxkbcommon-x11'
         'qt5-base' 'sdl_mixer' 'libircclient' 'tinyxml')
makedepends=('boost')
source=("$pkgname-$pkgver.tar.gz"
        "pokerth-1.1.2-boost-1.65-ambiguous-advance.patch"
)
sha512sums=('d54c84f199636eb5a53580213e5e4ccc828cf565bf4b055797daa82e2ad54d8c52c7a16a781b82c8cc89d184a2b3c4105922394d08c7d2e06383b96963c36b5a'
            '608bf93f861a9454c975bba2522fad97a7210585429026c5a2ae7236cc62e0370dae20db66d3888cfed716690c2deae7c2c7025438d84c451210a02719b1a385'
)

prepare() {
  cd "$pkgname-$_pkgver"

  patch -Np1 < "$srcdir"/pokerth-1.1.2-boost-1.65-ambiguous-advance.patch
}

build() {
  cd "$pkgname-$_pkgver"

  qmake-qt5 "$pkgname.pro" QMAKE_CFLAGS_ISYSTEM="" CONFIG+=client
  make
}

package() {
  cd "$pkgname-$_pkgver"

  # The make install target doesn't do shit anymore for some reason.
  make INSTALL_ROOT="$pkgdir" install

  # mkdir -p "$pkgdir"/usr/share/pokerth
  # cp -r data "$pkgdir"/usr/share/pokerth
  install -Dm644 "docs/pokerth.1" "$pkgdir/usr/share/man/man1/pokerth.1"
  install -Dm644 "data/data-copyright.txt" "$pkgdir/usr/share/licenses/pokerth/data-copyright.txt"
}
