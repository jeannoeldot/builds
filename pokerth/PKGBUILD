# Maintainer: Name <name@fai.com

pkgname=pokerth
pkgver=1.1.2
_pkgver=1.1.2-rc
pkgrel=33
pkgdesc="Open source Texas Hold'em Poker simulator"
arch=('i686' 'x86_64')
url="http://www.pokerth.net/"
license=('GPL' 'custom')
depends=('curl' 'boost-libs' 'gsasl' 'gnutls' 'protobuf' 'libxkbcommon-x11'
         'qt5-base' 'sdl_mixer' 'libircclient' 'tinyxml')
makedepends=('boost')
source=("$pkgname-$pkgver.tar.gz"
        "fix-build-with-boost.patch"
        "fix-build-with-protobuf.patch"
        "fix-build-websocketpp-with-boost.patch"
        "fix-build-to-explicitly-link.patch"
)
sha512sums=('d54c84f199636eb5a53580213e5e4ccc828cf565bf4b055797daa82e2ad54d8c52c7a16a781b82c8cc89d184a2b3c4105922394d08c7d2e06383b96963c36b5a'
            'b02e368dd6cc20e247c4297d3f9a9adcb8d062158ecc7c592d753b4beba737598971983b525313e8a6d5f20c9b0b8f9ce6c9fd37203d111d61ce15068e2408ae'
            '79e952b60e0688ff1bceea7558dd9f172f12c3bae3c9e973c4974971790ea329dfc376cfdbca932b6570c4001afb305a5568a34a8d752c758a0813a4db0c8c2b'
            '264f53197abcc0bc336f1a5f04392f866c9f629c3d07d6bcb110f7b0553a0659be014281b92f22d2a48e457bbbee9a75b672e4fe56a20e1e439b25abc99ef889'
            '4c0babf4fb8bccaa3be7db6460788f423f5164d95fda214c488c60c0a4941e9fb046cdad202131f03ef48d00427a659016f5d09620be2f685986c8e2266085e6'
)

prepare() {
  cd "$pkgname-$_pkgver"
  # Patchs boost V >= 1.75
  # see also DEFINE+="BOOST_BIND_GLOBAL_PLACEHOLDERS" in build below
  patch -Np1 < "$srcdir"/fix-build-with-boost.patch
  # Patch protobuf V >= 3.5.1.1
  patch -Np1 < "$srcdir"/fix-build-with-protobuf.patch
  # Patch websocketpp pour boost V >= 1.70 1.71
  patch -Np1 < "$srcdir"/fix-build-websocketpp-with-boost.patch
  # change to explicitly link /usr/lib/libabsl_log_internal_message.so and
  #                           /usr/lib/libabsl_log_internal_check_op.so
  patch -Np1 < "$srcdir"/fix-build-to-explicitly-link.patch
  
  # Rebuild files protobuf
  protoc -I="$srcdir"/"$pkgname-$_pkgver"/ "$srcdir"/"$pkgname-$_pkgver"/pokerth.proto --cpp_out="$srcdir"/"$pkgname-$_pkgver"/src/third_party/protobuf/
  protoc -I="$srcdir"/"$pkgname-$_pkgver"/ "$srcdir"/"$pkgname-$_pkgver"/chatcleaner.proto --cpp_out="$srcdir"/"$pkgname-$_pkgver"/src/third_party/protobuf/
}

build() {
  cd "$pkgname-$_pkgver"

  qmake-qt5 "$pkgname.pro" QMAKE_CFLAGS_ISYSTEM="" CONFIG+=client DEFINES+="BOOST_BIND_GLOBAL_PLACEHOLDERS"
  make
}

package() {
  cd "$pkgname-$_pkgver"

  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 "docs/pokerth.1" "$pkgdir/usr/share/man/man1/pokerth.1"
  install -Dm644 "data/data-copyright.txt" "$pkgdir/usr/share/licenses/pokerth/data-copyright.txt"
  rm -f "$pkgdir/usr/share/pokerth/data/data-copyright.txt"
}
