# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Eschwartz <eschwartz93@gmail.com>

pkgname=firefox-adblock-plus
pkgver=2.7
pkgrel=1
pkgdesc="Extension for firefox which block ads and banners"
arch=('any')
url="http://adblockplus.org/"
license=('GPL')
groups=('firefox-addons')
depends=("firefox")
makedepends=('unzip')
source=("https://downloads.adblockplus.org/adblockplus-$pkgver.xpi")
noextract=("adblock-plus-$pkgver.xpi")
md5sums=('c761cb56de53b9ee0c28212139144afc')

prepare() {
  cd "${srcdir}"
  unzip -qqo "adblockplus-${pkgver}.xpi" -d "adblockplus-${pkgver}"
}

package() {
  cd "${srcdir}"

  _extension_id="$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' adblockplus-${pkgver}/install.rdf)"
  _extension_dest="${pkgdir}/usr/lib/firefox/browser/extensions/${_extension_id}"
  if grep '<em:unpack>true</em:unpack>' adblockplus-${pkgver}/install.rdf > /dev/null; then
    install -dm755 "${_extension_dest}"
    cp -R adblockplus-${pkgver}/* "${_extension_dest}"
    chmod -R ugo+rX "${_extension_dest}"
  else
    install -Dm644 adblockplus-${pkgver}.xpi "${_extension_dest}.xpi"
  fi
}
