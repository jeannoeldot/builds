# Maintainer: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Based on PKGBUILD firefox-adblock-plus
# Maintainer: Jean-Noel Dot <jean.noel.dot AT sfr DOT fr>

_pkgname=https-everywhere
pkgname=firefox-${_pkgname}
pkgver=5.1.4
pkgrel=1
pkgdesc="Plugin for firefox which ensures you are using https whenever it's possible."
arch=('any')
url="https://www.eff.org/https-everywhere"
license=('GPL2')
depends=("firefox")
makedepends=("unzip")
source=("${_pkgname}-${pkgver}.xpi"
)
noextract=("${_pkgname}-${pkgver}.xpi")
sha256sums=('0e2cd19dfa5fedfbfa374971d804951ce4177a6ce44a048c6b43e19bbecf4534'
)

prepare() {
  cd "${srcdir}"

  # Ugly hack, bsdtar does not extract the xpi properly...
  unzip -qqo "${_pkgname}-${pkgver}.xpi"  -d "${_pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}"

  _extension_id="$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' ${_pkgname}-${pkgver}/install.rdf)"
  _extension_dest="${pkgdir}/usr/lib/firefox/browser/extensions/${_extension_id}"
  if grep '<em:unpack>true</em:unpack>' ${_pkgname}-${pkgver}/install.rdf > /dev/null; then
    install -dm755 "${_extension_dest}"
    cp -R ${_pkgname}-${pkgver}/* "${_extension_dest}"
    chmod -R ugo+rX "${_extension_dest}"
  else
    install -Dm644 ${_pkgname}-${pkgver}.xpi "${_extension_dest}.xpi"
  fi
}
