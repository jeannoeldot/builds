# Maintainer: Name <name@fai.com>

pkgname=alembic-jnd
pkgver=1.8.9.20251003
pkgrel=1
_ver_release="1.8.9"
_name_release="alembic-${_ver_release}"
pkgdesc="An open framework for storing and sharing scene data"
url="http://www.alembic.io/"
arch=('x86_64')
license=('BSD-2-Clause')
depends=('gcc-libs' 'imath'
)
makedepends=('cmake'
)
conflicts=('alembic' 'alembic-git')
provides=('alembic')
options=('!debug')
source=("alembic-${_ver_release}.tar.gz"
)
sha256sums=('8c59c10813feee917d262c71af77d6fa3db1acaf7c5fecfd4104167077403955'
)

build() {
  cd "${_name_release}"

  cmake \
    -Bbuild \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS_RELEASE:STRING=-DNDEBUG \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_TESTS=OFF
  sed -i "s/\-O3//g" $(find . -name flags.make)
  make -C build
}

package() {
  cd "${_name_release}"
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  
  DESTDIR="${pkgdir}" make -C build install
}
